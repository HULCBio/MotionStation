#  $Revision: 1.2 $

use msvc_modules_installer;

sub msvc70opts 
{
    my $input = shift;
    my $default_location = "C:\\Program Files\\Microsoft Visual Studio .NET";

    my $specified_compiler_location = "";

    my $language_handled_fcn = sub {
        my $lang = shift;
        
        return $lang eq "C";
    };

    my $locate_fcn = sub {
        my @msvc7_roots = ();
        my $msvc7_root;
        my $registry_lookup_fcn = $input->{"registry_lookup"};

        if ($ENV{'MSVCDir'} ne "" && $ENV{'MSVCDir'} =~ /\\Vc7$/i &&
            -e "$ENV{'MSVCDir'}\\bin\\cl.exe")
        {
            $msvc7_root = $ENV{'MSVCDir'};
            $msvc7_root =~ s|\\Vc7$||i;
            push(@msvc7_roots, $msvc7_root);
        }

        $msvc7_root = &$registry_lookup_fcn("SOFTWARE\\Microsoft\\VisualStudio\\7.0\\" . 
                                            "Setup\\VC", "ProductDir");

        if (-e "$msvc7_root\\bin\\cl.exe")
        {
            $msvc7_root =~ s|\\Vc7\\$||i;
            push(@msvc7_roots, $msvc7_root);
        }

        if (-e "$default_location")
        {
            push(@msvc7_roots, $default_location);
        }

        return @msvc7_roots;
    };

    my $root_val = sub {
        my $base_directory = shift;

        $specified_compiler_location = $base_directory; # Remember this for post_setup_hook
        if (!-d "$base_directory\\Common7")
        {       
            print "Warning: Mex requires that the Microsoft Visual C++ 7.0\n" .
                "directories \"Vc7\" and \"Common7\" be located within the same parent " .
                    "directory.\n(Expected to find a directory named \"Common7\" in the directory '$base_directory'.)";
        }

        # Since the Microsoft Visual C environment variable MSVCDir actually points to the
        # Vc7 subdirectory of MSVC 7.0, we need to tack on the \Vc7 to the base compiler
        # location.
        return $base_directory . "\\Vc7";
    };

    return {
        "vendor_name"      => "Microsoft Visual C/C++",
        "version"          => "7.0",
        "group_id"         => "MSVC",
        "serial"           => 4.0,
        "root_var"         => "MSVCDir",
        "optfile_name"     => "msvc70opts.bat",
        "default_location" => $default_location,
        "language_handled" => $language_handled_fcn,
        "root_val"         => $root_val,
        "locate"           => $locate_fcn,
        "post_setup_hook"  => $post_setup_hook
        };
}
1;
