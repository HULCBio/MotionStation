%% $Revision: 1.1.6.2 $
%% 
%%
%% Copyright 1994-2003 The MathWorks, Inc.
%%
%% Abstract: Data Store Read block target file

%implements DataStoreRead "C"

%% Function: BlockInstanceSetup ==============================================
%% Abstract:
%%   Set expression folding compliance
%%
%function BlockInstanceSetup(block, system) void
  %<LibBlockSetIsExpressionCompliant(block)>
%endfunction


%% Function: Outputs ==========================================================
%% Abstract:
%%   Y[i] = DWorkSrc[i]
%%
%% Why DWORK is not declared in rollVars? Why did we pass lcv instead as ucv
%%
%function Outputs(block, system) Output
  %assign  dworkSrc = DWorkSrc
  %assign rollVars = ["Y"]
  %roll sigIdx = RollRegions, lcv = RollThreshold, block, "Roller", rollVars
    %assign y = LibBlockOutputSignal(0, "", lcv, sigIdx)
    %assign u = LibBlockDWork(dworkSrc, lcv, "", sigIdx)
    %<y> = %<u>;
  %endroll

%endfunction



%% Function: BlockOutputSignal =================================================
%% Abstract:
%%      Return the appropriate reference to the datastore.  This function *may*
%%      be used by Simulink when optimizing the Block IO data structure.
%%
%function BlockOutputSignal(block,system,portIdx,ucv,lcv,idx,retType) void
  %switch retType
    %case "Signal"
      %assign dworkSrc = DWorkSrc
      %assign dsm = LibBlockDWork(dworkSrc, ucv, lcv, idx)
      %return dsm
      %%START_ASSERT
    %default
      %assign errTxt = "Unsupported return type: %<retType>"
      %<LibBlockReportError(block,errTxt)>
      %%END_ASSERT
  %endswitch
%endfunction




%% [EOF] dsread.tlc
