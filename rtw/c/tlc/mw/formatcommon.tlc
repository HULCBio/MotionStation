%% ============================================================================
%% file: formatcommon.tlc
%% $Revision: 1.1.6.7 $ 
%% $Date: 2004/04/14 23:48:14 $
%%
%% Copyright 1994-2003 The MathWorks, Inc.
%% 

%% Function:  DumpUserCodeAndCustomDataInst  ===================================
%% Dump user prm in the base file
%%
%function DumpUserCodeAndCustomDataInst() void
  %assign baseFile = GetBaseFile("SystemBody")
  
  %%  
  %% Custom prm header code - This code used to go at the top of the _prm file,
  %% it now goes at the bottom of the includes section of the model.c file.
  %%
  %if !Accelerator && !IsModelReferenceSimTarget()
    %openfile tmpBuf
    %<LibDumpTopOfPrmFileUserCode()>\
    %closefile tmpBuf
    %<SLibSetModelFileAttribute(baseFile, "Includes", tmpBuf)>

    %%
    %% Custom prm trailer code - this used to go at the bottom of the _prm.h file,
    %% it now goes as the bottom of the "Defines" section of model.c
    %%
    %openfile tmpBuf
    %<LibDumpBottomOfPrmFileUserCode()>\
    %closefile tmpBuf
    %<SLibSetModelFileAttribute(baseFile, "Defines", tmpBuf)>
  
    %% Custom user code for bottom of file.  For new RTW S-function, 
    %% put this code at the top of the top-level subsystem file.
    %%
    %openfile tmpBuf
    %<LibDumpBottomOfSourceFileUserCode()>\
    %closefile tmpBuf
    %<SLibSetModelFileAttribute(baseFile, "UserBottom", tmpBuf)>

    %% Custom user code for top of file
    %%
    %openfile tmpBuf
    %<LibDumpTopOfSourceFileUserCode()>\
    %closefile tmpBuf
    %<SLibSetModelFileAttribute(baseFile, "UserTop", tmpBuf)>
  %endif  

  %openfile tmpBuf
  %% Global block instance data (Cached by blocks in BlockInstanceSetup)
  %%
  %assign reqInsts = LibGetSystemField(System[NumSystems-1], "ReqRootPrmHdrDataInsts")
  %if reqInsts.GlobalPrmDataCacheInst
    %<LibDumpGlobalPrmDataCache()>\
  %endif
  %closefile tmpBuf
  %<SLibSetModelFileAttribute(baseFile, "Definitions", tmpBuf)>
  
%endfunction %% DumpUserCodeAndCustomDataInst

%% Function:  DumpExportFileUserCode ==========================================
%%
%function DumpExportFileUserCode() void
  %assign isSfcnForm = (CodeFormat == "S-Function")
  
  %if !isSfcnForm && !Accelerator && !IsModelReferenceSimTarget()
    %assign baseFile = GetBaseFile("SystemHeader")

    %openfile tmpBuf
    %<SLibDumpTopOfExportFileUserCode()>\
    %closefile tmpBuf
    %<SLibSetModelFileAttribute(baseFile, "UserTop", tmpBuf)>
    
    %openfile tmpBuf
    %<SLibDumpBottomOfExportFileUserCode()>\
    %closefile tmpBuf
    %<SLibSetModelFileAttribute(baseFile, "UserBottom", tmpBuf)>
    
  %endif
%endfunction %% DumpExportFileUserCode


%% Function:  DumpExportedDataInstance ========================================
%%
%function DumpExportedDataInstance() void
  
  %assign reqInsts = LibGetSystemField(System[NumSystems-1], "ReqRootPrmHdrDataInsts")
  
  %openfile expInst
  %% Exported Global (User) Signals
  %%
  %if reqInsts.ExpGlobalSigsInst && !LibExportedGlobalSignalsInstanceIsEmpty()
    
    /* Exported block signals */
    %<LibDumpExportedGlobalSignalsInstance()>\
  %endif
  
  %% Exported Global (User) Parameters
  %%
  %if reqInsts.ExpGlobalPrmsInst && !SLibPrmBufferIsEmpty("ExportedGlobal", "Instance")
    
    /* Exported block parameters */
    %<SLibGetPrmBuffer("ExportedGlobal", "Instance")>
  %endif
  
  %% Exported Global (User) States
  %%
  %if reqInsts.ExpGlobalDworkInst && !LibExportedGlobalDWorkInstanceIsEmpty()
    
    /* Exported block states */
    %<LibDumpExportedGlobalDWorkInstance()>\
  %endif
 
  %closefile expInst
  
  %if !WHITE_SPACE(expInst)
    %assign baseFile = GetBaseFile("SystemBody")
    %<SLibSetModelFileAttribute(baseFile, "Definitions", expInst)>
  %endif
%endfunction %%DumpExportedDataInstance

%% Function:  DumpExportedDataDefinition ======================================
%%
%function DumpExportedDataDefinition() void
  %if !Accelerator && !IsModelReferenceSimTarget()
    %assign isSfcnForm = (CodeFormat == "S-Function")
  
    %assign baseFile = GetBaseFile("SystemHeader")
  
    %% Exported signals
    %%
    %openfile expDef
    %if !SLibExportedGlobalSignalsIsEmpty()
      
      /*
      * Exported Global Signals
      *
      * Note: Exported global signals are block signals with an exported global
      * storage class designation.  RTW declares the memory for these signals
      * and exports their symbols.
      *
      */
      %<SLibDumpExportedGlobalSignalsDefinition()>\
    %endif
    
    %% Exported global parameters
    %%
    %if !SLibPrmBufferIsEmpty("ExportedGlobal", "Decl")
      /*
      * Exported Global Parameters
      *
      * Note: Exported global parameters are tunable parameters with an exported
      * global storage class designation.  RTW declares the memory for these
      * parameters and exports their symbols.
      *
      */
      %<SLibGetPrmBuffer("ExportedGlobal", "Decl")>\
    %endif
 
    %if !isSfcnForm
      %% Exported dwork
      %%
      %if !SLibExportedGlobalDWorkIsEmpty()
	/*
	* Exported States
	*
	* Note: Exported states are block states with an exported global
	* storage class designation.  RTW declares the memory for these states
	* and exports their symbols.
	*
	*/
	%<SLibDumpExportedGlobalDWorkDefinition()>\
      %endif
    %endif
    %closefile expDef
    
    %<SLibSetModelFileAttribute(baseFile, "ExternData", expDef)>  
  %endif
%endfunction

%% Function: DumpHdrFileUserCode  =============================================
%% Abstract: Dump the user code
%%
%function DumpHdrFileUserCode() void
  %if !Accelerator && !IsModelReferenceSimTarget()
    %assign baseFile = GetBaseFile("SystemHeader")
    %% User top
    %openfile tmpBuf
    %<LibDumpTopOfHdrFileUserCode()>\
    %closefile tmpBuf
    %<SLibSetModelFileAttribute(baseFile, "UserTop", tmpBuf)>
    
    %% User bottom
    %openfile tmpBuf
    %<LibDumpBottomOfHdrFileUserCode()>\
    %closefile tmpBuf
    %<SLibSetModelFileAttribute(baseFile, "UserBottom", tmpBuf)>
  %endif
%endfunction %% DumpHdrFileUserCode


%% Function: DumpAllSystemHeadersExcludingRootSystem  ===========================
%% Abstract: 
%%    Dump all system headers in the system file, and return the headers
%%    that must be dumped in model.h
%%
%function DumpAllSystemHeadersExcludingRootSystem()
  %openfile nonVirtSubsbys
  %foreach sysIdx = NumSystems-1 %% root not included
    %assign system = System[sysIdx]
    %<LibDumpSystemHeader(system)>\
  %endforeach
  %closefile nonVirtSubsbys
  %return nonVirtSubsbys
%endfunction %%DumpAllSystemHeadersExcludingRootSystem