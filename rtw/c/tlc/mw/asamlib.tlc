%% 
%% $Revision: 1.1.6.2 $
%% 
%%
%% Copyright 1994-2003 The MathWorks, Inc.
%%
%% Description:
%%   Support library for ASAM-MCDx support.
%%

%if EXISTS("_ASAMLIB_") == 0
%assign _ASAMLIB_ = 1

%%
%%  Block defaults contains:
%%
%%  BlockDefaults {
%%    NumParameterGroups 0
%%  }
%%  
%%  Create a parameter group in BlockInstanceSetup:
%%  
%%  %assign group     = SLibCreateParameterGroup(block,"Lookup1D")
%%  %assign mainParam = SLibAddMainParameter(block,group,YData)>
%%  %assign subParam  = SLibAddSubParameter(block,mainParam,XData)>
%%
%%  This example defines the XData as a sub-parameter of the YData, and
%%  the block's group definition is as follows:
%%
%%  Block {
%%    Type "Lookup2D"
%%    Parameter {
%%    	Name "XData"
%%    	MemoryMapIdx [1 2 3]
%%    }
%%    Parameter {
%%    	Name "YData"
%%    	MemoryMapIdx [1 2 4]
%%    }
%%    Parameter {
%%    	Name "ZData"
%%    	MemoryMapIdx [1 2 5]
%%    }
%%    NumParameterGroups 1
%%    ParameterGroup {
%%    	Name "2DLookup"
%%    	NumMembers 1
%%    	Member {
%%    	  Reference  ZData
%%    	  NumMembers 2
%%    	  Member {
%%    	    Reference  XData
%%    	    NumMembers 0
%%    	  }
%%    	  Member {
%%    	    Reference  YData
%%    	    NumMembers 0
%%    	  }
%%    	}
%%    }
%%  }
%%


%% Function: SLibCreateParameterGroup ===========================================
%% Abstract:
%%   Create (add) a parameter group to a block.  For example,
%%
%%   %assign group = SLibAddCreateParameterGroup(block,"2DLookup")
%%
%%   Block {
%%     ParameterGroup {
%%       Name        "2DLookup"
%%       NumMembers 0
%%     }
%%   }
%%

%function SLibCreateParameterGroup(block,name) void
  %if !EXISTS("block.NumParameterGroups")
    %<LibAddIdentifier(block,"NumParameterGroups",1)>
  %else
    %assign block.NumParameterGroups = NumParameterGroups + 1
  %endif
  %assign tmpVar = ParameterGroup { Name name; NumMembers 0 }
  %assign block = block + ParameterGroup
  %return block.ParameterGroup[NumParameterGroups-1]
%endfunction


%% Function: SLibAddMember ======================================================
%% Abstract:
%%   Add member reference to a ParameterGroup or ParameterData.
%%   See lookup2d.tlc for an example usage of this function.
%%
%%   ParameterGroup {
%%     Name "2DLookup"
%%     NumMembers 1
%%     Member {               <== %assign m = SLibAddMember(block,group,ZData)
%%       Reference  ZData
%%       NumMembers 2
%%       Member {             <== %assign t = SLibAddMember(block,m,XData)
%%         Reference  XData
%%         NumMembers 0
%%       }
%%       Member {             <== %assign t = SLibAddMember(block,m,YData)
%%         Reference  YData
%%         NumMembers 0
%%       }
%%     }
%%   }
%%

%function SLibAddMember(block,group,param) void
  %assign tmpVar = Member { Reference param ; NumMembers 0 }
  %assign group = group + Member
  %assign group.NumMembers = group.NumMembers + 1
  %return group.Member[group.NumMembers-1]
%endfunction

%endif %% _ASAMLIB_

%% [EOF] asamlib.tlc
