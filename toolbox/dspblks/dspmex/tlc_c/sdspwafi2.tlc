%% $RCSfile: sdspwafi2.tlc,v $
%% $Revision: 1.6.4.2 $ 
%% $Date: 2004/04/12 23:10:12 $
%%
%% Copyright 1995-2003 The MathWorks, Inc.
%%
%% Abstract: Gets audio samples from .wav audio file

%implements sdspwafi2 "C"

%include "dsplib.tlc"

%% Function: BlockTypeSetup ==============================================
%function BlockTypeSetup(block, system) void

    %<Include_rt_header(block,"fromwavefile_ex_win32")>

	%% copy needed source files to project directory and add to build
	
	%assign helper_source = "fromwavefile_ex_win32"
	%assign utils_source  = "audio_utils_win32"
	%assign helper_c      = helper_source + ".c"
	%assign utils_c       = utils_source + ".c"

	%assign file_sep      = FEVAL("filesep")
	%assign source_dir    = FEVAL("fullfile",MATLAB_ROOT,"toolbox","dspblks","include",file_sep)
	%assign model_dir     = FEVAL("rtwprivate","rtwattic","getBuildDir") + file_sep

	%if ! LibAddToModelSources(helper_source) %% only copy it if it's not there already
		%<FEVAL("copyfile", "%<source_dir>%<helper_c>", "%<model_dir>%<helper_c>")>
	%endif

	%if ! LibAddToModelSources(utils_source)
		%<FEVAL("copyfile", "%<source_dir>%<utils_c>", "%<model_dir>%<utils_c>")>
	%endif

%endfunction %% BlockTypeSetup

%% Function: BlockInstanceSetup ==============================================
%%
%function BlockInstanceSetup(block, system) void

  %assign OUTPORT	    = 0

  %assign xfilename		= "\"" + SFcnParamSettings.Filename + "\""
  %assign filename		= FEVAL("strrep", xfilename, "\\", "\\\\") 
  %assign bits			= SFcnParamSettings.BitsPerSample
  %assign minRead		= SFcnParamSettings.MinSampsToRead
  %assign chans			= SFcnParamSettings.NumChannels
  %assign frameSize		= SFcnParamSettings.OutputFrameSize
  %assign rate			= SFcnParamSettings.SampleRate
  %assign dataType		= LibBlockOutputSignalDataTypeId(OUTPORT)
  %assign loopOrNot		= SFcnParamSettings.LoopOrNot
  %assign numRepeats	= SFcnParamSettings.NumRepeats
  %assign repeatMode	= SFcnParamSettings.RepeatMode
  %assign numOutputPorts= SFcnParamSettings.NumOutputPorts
  %assign outputFirstSample = SFcnParamSettings.OutputFirstSample
  %assign outputLastSample  = SFcnParamSettings.OutputLastSample
  %assign lastSampOutputIdx = SFcnParamSettings.LastSampleOutputIdx
  %assign FIRST_OUTPORT = 1
  %%

  %assign block = block + OUTPORT + filename + bits + minRead + chans + dataType ...
        + frameSize + rate + loopOrNot + numRepeats + repeatMode + numOutputPorts ...
		+ lastSampOutputIdx + FIRST_OUTPORT + outputFirstSample + outputLastSample

%endfunction %% BlockInstanceSetup


%% Function: Start ==========================================================
%%
%function Start(block, system) Output

	/* Signal Processing Blockset From Wave File (%<ParamSettings.FunctionName>) - '%<Name>' - Start */
	%<LibBlockDWork(FromWaveFileObj, "", "", 0)> = exMWDSP_Wafi_Create(%<filename>, 
				%<bits>, %<minRead>, %<chans>, %<frameSize>, %<rate>, %<dataType>);
	%if loopOrNot == 1 && numRepeats != 0
		exMWDSP_Wafi_SetNumRepeats(%<LibBlockDWork(FromWaveFileObj, "", "", 0)>, (long) %<numRepeats>);
		exMWDSP_Wafi_SetRestartMode(%<LibBlockDWork(FromWaveFileObj, "", "", 0)>, %<repeatMode>);
	%endif

%endfunction %% Start




%% Function: Outputs ==========================================================
%%
%function Outputs(block, system) Output

	/* Signal Processing Blockset From Wave File (%<ParamSettings.FunctionName>) - '%<Name>' - Output */
	exMWDSP_Wafi_Outputs(%<LibBlockDWork(FromWaveFileObj, "", "", 0)>, %<LibBlockOutputSignalAddr( OUTPORT,  "", "", 0)>);

	%if outputFirstSample == 1 && numOutputPorts >= 2
		*(boolean_T*)%<LibBlockOutputSignalAddr(FIRST_OUTPORT,  "", "", 0)> = 
			(boolean_T) exMWDSP_Wafi_JustOutputFirstSample(%<LibBlockDWork(FromWaveFileObj, "", "", 0)>);
	%endif

	%if outputLastSample == 1 && numOutputPorts >= 2
		*(boolean_T*)%<LibBlockOutputSignalAddr(lastSampOutputIdx,  "", "", 0)> = 
			(boolean_T) exMWDSP_Wafi_JustOutputLastSample(%<LibBlockDWork(FromWaveFileObj, "", "", 0)>);
	%endif

%endfunction %% Outputs



%% Function: Update ==========================================================
%%
%function Update(block, system) Output

%endfunction %% Update




%% Function: Terminate ==========================================================
%%
%function Terminate(block, system) Output

	/* Signal Processing Blockset From Wave File (%<ParamSettings.FunctionName>) - '%<Name>' - Terminate */
    exMWDSP_Wafi_Terminate(%<LibBlockDWork(FromWaveFileObj, "", "", 0)>);

%endfunction %% Terminate



%% [EOF] sdspwafo2.tlc
