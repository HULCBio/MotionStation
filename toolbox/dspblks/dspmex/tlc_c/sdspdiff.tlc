%% $RCSfile: sdspdiff.tlc,v $
%% $Revision: 1.12.4.2 $ 
%% $Date: 2004/04/12 23:08:25 $
%%
%% Copyright 1995-2003 The MathWorks, Inc.
%%
%% Abstract: Element-to-element difference along a vector

%implements sdspdiff "C"

%% Function: Outputs ==========================================================
%%
%function Outputs(block, system) Output
    /* Signal Processing Blockset Difference (%<ParamSettings.FunctionName>) - %<Name> */
    %%
    %assign INPORT  = 0
    %assign OUTPORT = 0
    %assign c0      = LibBlockOutputSignalIsComplex(OUTPORT)
    %assign inplace = (LibBlockInputSignalBufferDstPort(INPORT) == OUTPORT)
    %assign contig  = IsInputPortContiguous(block,INPORT)
    %assign N       = LibDataInputPortWidth(INPORT)
    %%
    %if inplace
        /* In-place algorithm */
        %if !c0
           %% REAL
           {
            real_T *y1 = %<LibBlockOutputSignalAddr(OUTPORT,"","",N-1)>;
            real_T *y  = y1-1;
            int     N  = %<N>;
            while(--N > 0) {
                *y1-- -= *y--;
            }
           }
        %else
           %% COMPLEX
           {
             creal_T  *y1 = %<LibBlockOutputSignalAddr(OUTPORT,"","",N-1)>;
             creal_T  *y  = y1-1;
             int       N  = %<N>;
             while(--N > 0) {
                y1->re     -= y->re;
                (y1--)->im -= (y--)->im;
             }
           }
        %endif
    %%
    %elseif contig
    %%
        %% Contiguous
        %if !c0
           %% REAL
           {
            real_T *u1 = %<LibBlockInputSignalAddr(INPORT, "", "", N-1)>;
            real_T *u  = u1-1;
            real_T *y  = %<LibBlockOutputSignalAddr(OUTPORT,"","",N-1)>;
            int     N  = %<N>;
            while(--N > 0) {    
                *y-- = *u1-- - *u--;
            }
           }
        %else
           %% COMPLEX
           {
            creal_T  *u1 = %<LibBlockInputSignalAddr(INPORT, "", "", N-1)>;
            creal_T  *u  = u1-1;
            creal_T  *y  = %<LibBlockOutputSignalAddr(OUTPORT,"","",N-1)>;
            int       N  = %<N>;
            while(--N > 0) {
               y->re     = u1->re - u->re;
               (y--)->im = (u1--)->im - (u--)->im;
            }
           }
        %endif
        %%
      %else
	
	%<LibDiscontiguousInputError(block)>

      %endif

%endfunction

%% [EOF] sdspdiff.tlc

