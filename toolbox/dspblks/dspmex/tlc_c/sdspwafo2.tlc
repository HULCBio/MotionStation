%% $RCSfile: sdspwafo2.tlc,v $
%% $Revision: 1.5.4.2 $ 
%% $Date: 2004/04/12 23:10:13 $
%%
%% Copyright 1995-2003 The MathWorks, Inc.
%%
%% Abstract: Creates WAVE audio (.wav) file from input.

%implements sdspwafo2 "C"

%include "dsplib.tlc"

%% Function: BlockTypeSetup ==============================================
%function BlockTypeSetup(block, system) void

    %<Include_rt_header(block,"towavefile_ex_win32")>
	
	%% copy needed source files to project directory and add to build
	
	%assign helper_source = "towavefile_ex_win32"
	%assign utils_source  = "audio_utils_win32"
	%assign helper_c      = helper_source + ".c"
	%assign utils_c       = utils_source + ".c"

	%assign file_sep      = FEVAL("filesep")
	%assign source_dir    = FEVAL("fullfile",MATLAB_ROOT,"toolbox","dspblks","include",file_sep)
	%assign model_dir     = FEVAL("rtwprivate","rtwattic","getBuildDir") + file_sep

	%if ! LibAddToModelSources(helper_source) %% only copy it if it's not there already
		%<FEVAL("copyfile", "%<source_dir>%<helper_c>", "%<model_dir>%<helper_c>")>
	%endif

	%if ! LibAddToModelSources(utils_source)
		%<FEVAL("copyfile", "%<source_dir>%<utils_c>", "%<model_dir>%<utils_c>")>
	%endif

%endfunction %% BlockTypeSetup

%% Function: BlockInstanceSetup ==============================================
%%
%function BlockInstanceSetup(block, system) void

  %assign INPORT	    = 0
  %%
  %assign bitsPerSample	    = SFcnParamSettings.BitsPerSample
  %assign xoutputFilename    = "\"" + SFcnParamSettings.Filename + "\""
  %assign outputFilename  = FEVAL("strrep", xoutputFilename, "\\", "\\\\") 
  %assign minSampsToWrite   = SFcnParamSettings.MinSamplesToWrite
  %%
  %assign numDims   = LibBlockInputSignalNumDimensions(INPORT)
  %assign dims      = LibBlockInputSignalDimensions(INPORT)
  %assign isFrame   = LibBlockInputSignalIsFrameData(INPORT)
  %if isFrame
    %assign inputBufSize = dims[0]
    %assign numChannels  = (numDims == 2) ? dims[1] : 1
  %else
    %assign inputBufSize = 1
    %assign numChannels  = LibBlockInputSignalWidth(INPORT)
  %endif

  %assign sampleRate	 = inputBufSize / LibBlockSampleTime(block)
  
  %assign dataTypeID  = LibBlockInputSignalDataTypeId(   INPORT)

  %assign block = block + INPORT + outputFilename + bitsPerSample + minSampsToWrite ... 
		+ inputBufSize + numChannels  + sampleRate  + dataTypeID

%endfunction %% BlockInstanceSetup



%% Function: Start ==========================================================
%%
%function Start(block, system) Output

	/* Signal Processing Blockset To Wave File (%<ParamSettings.FunctionName>) - '%<Name>' - Start */
	%<LibBlockDWork(ToWaveFileObj, "", "", 0)> = exMWDSP_Wafo_Create(%<outputFilename>, %<bitsPerSample>, 
		    %<minSampsToWrite>, %<numChannels>, %<inputBufSize>, %<sampleRate>, %<dataTypeID>);

%endfunction %% Start




%% Function: Outputs ==========================================================
%%
%function Outputs(block, system) Output

	/* Signal Processing Blockset To Wave File (%<ParamSettings.FunctionName>) - '%<Name>' - Output */    
    exMWDSP_Wafo_Outputs(%<LibBlockDWork(ToWaveFileObj, "", "", 0)>, %<LibBlockInputSignalAddr( INPORT,  "", "", 0)>);

%endfunction %% Outputs



%% Function: Terminate ==========================================================
%%
%function Terminate(block, system) Output

	/* Signal Processing Blockset To Wave File (%<ParamSettings.FunctionName>) - '%<Name>' - Terminate */
    exMWDSP_Wafo_Terminate(%<LibBlockDWork(ToWaveFileObj, "", "", 0)>);

%endfunction %% Terminate



%% [EOF] sdspwafo2.tlc
