%% $RCSfile: sfun_user_fxp_prodsum.tlc,v $
%% $Revision: 1.1.6.1 $
%% $Date: 2003/03/05 07:08:11 $

%% Copyright 1994-2003 The MathWorks, Inc.
%%
%% Abstract:
%%     User SFunction Fixed-point ProdSum

%implements sfun_user_fxp_prodsum "C"

%include "fix_published.tlc"

%function BlockInstanceSetup(block, system) void
  %%
%endfunction 


%function BlockTypeSetup(block, system) void
  %%
  %openfile buffer
#if ( SHRT_MAX != 32767 )
#error The interface from Simulink to the legacy C code assumes short is 16 bits, but this was not true.
#endif  

#if ( INT_MAX != 2147483647 )
#error The interface from Simulink to the legacy C code assumes int is 32 bits, but this was not true.
#endif  

static short fixpt_prodsum(const short *b, const short *x, int L, int shift_out) {
    long LSB_BY_2 = 1<<(shift_out-1);
    long ACC;
    int i;

    ACC = x[L-1] * b[0];
    for (i=1; i<L; i++) ACC += b[i] * x[L-1-i];
    return ((short)((ACC + LSB_BY_2) >> shift_out));    
}
  %closefile buffer    
  %<SLibCacheSourceCode(buffer)>\
%endfunction


%function Outputs(block, system) Output
  %%
  %%  Put comment into the generated C code
  %%
  /* User Sfunction fixed-point prodsum */
  %%
  %assign labelForU0Addr = LibBlockInputSignalAddr( 0,  "", "", 0 )
  %assign labelForU1Addr = LibBlockInputSignalAddr( 1,  "", "", 0 )
  %assign labelForY0 = LibBlockOutputSignal(0,     "", "", 0 )
  %%
  %assign dataTypeIdY0 = LibBlockOutputSignalDataTypeId(0)
  %assign dataTypeIdU0 = LibBlockInputSignalDataTypeId(0)
  %assign dataTypeIdU1 = LibBlockInputSignalDataTypeId(1)
  %%
  %assign fractionLengthY0 = FixPt_DataTypeFractionLength( block, dataTypeIdY0 )
  %assign fractionLengthU0 = FixPt_DataTypeFractionLength( block, dataTypeIdU0 )
  %assign fractionLengthU1 = FixPt_DataTypeFractionLength( block, dataTypeIdU1 )
  %%
  %assign shift_out = fractionLengthU0 + fractionLengthU1 - fractionLengthY0
  %%
  %assign width = LibDataInputPortWidth(0)
  %%
  %<labelForY0> = fixpt_prodsum( %<labelForU0Addr>, %<labelForU1Addr>, %<width>, %<shift_out> );
  %%
  %% blank line for formating

%endfunction
