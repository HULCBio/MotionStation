%% ============================================================================
%% $RCSfile: xpcoptions.tlc,v $
%% $Revision: 1.11.2.6 $
%% $Date: 2004/04/08 21:01:28 $
%%
%% Copyright 1996-2004 The MathWorks, Inc.
%%

%if EXISTS("_RLOPTIONS_") == 0
%assign _RLOPTIONS_ = 1

%realformat "CONCISE"

%with CompiledModel

%selectfile STDOUT
%if RTWVerbose
### Creating xPC Options Header file xpcoptions.h.
%endif
%selectfile NULL_FILE

%assign xpcOptHdrDef = "__" "%<CompiledModel.Name>" "_XPCOPTIONS_H___"

%openfile RLOptionsDotHeader = "xpcoptions.h"
#ifndef %<xpcOptHdrDef>
#define %<xpcOptHdrDef>

#include "simstruc_types.h"
#ifndef MT
#define MT 0                    /* MT may be undefined by simstruc_types.h */
#endif

#include "%<CompiledModel.Name>.h"

%if !EXISTS("xpcDblBuff")
  %assign xpcDblBuff=0
%endif

%assign sizeofParams = "sizeof(Parameters_%<CompiledModel.Name>)"
%if xpcDblBuff != 1
  %assign sizeofParams = "(-1 * %<sizeofParams>)"
%endif
%%
%if CompiledModel.ModelParameters.NumInrtP==0
  %assign sizeofParams = 0
%endif

#define SIZEOF_PARAMS %<sizeofParams>

%if RL32ModeModifier=="Real-Time"
#define SIMMODE          0
%endif
%if RL32ModeModifier=="Freerun"
#define SIMMODE          1
%endif

%if RL32LogTETModifier==1
#define LOGTET           1
%endif
%if RL32LogTETModifier==0
#define LOGTET           0
%endif

#define LOGBUFSIZE       %<RL32LogBufSizeModifier>

%if TYPE(RL32IRQSourceModifier)=="String" && RL32IRQSourceModifier=="Timer"
#define IRQ_NO          0
#define IO_IRQ          0
%else
#define IRQ_NO          %<RL32IRQSourceModifier>
#define IO_IRQ          1
%endif

%if !EXISTS("xpcWWWAccessLevel")
  %assign xpcWWWAccessLevel = 0
%endif

#define WWW_ACCESS_LEVEL %<xpcWWWAccessLevel>

%if !EXISTS("xpcCPUClockPoll")
  %assign xpcCPUClockPoll = 0
%endif

#define CPUCLOCK %<xpcCPUClockPoll>


%<FcnGenChangeStepSizesFcn()>

void XPCCALLCONV changeStepSize(real_T stepSize) {
  %<FcnCallChangeStepSizesFcn("stepSize")>
}

#endif /* %<xpcOptHdrDef> */

%closefile RLOptionsDotHeader

%selectfile NULL_FILE

%openfile RLOptionsDotM = "xpcoptions.m"

function info=xpcoptions
info.objname='%<RL32ObjectName>';
info.xpcObjCom=%<xpcObjCom>;
%closefile RLOptionsDotM

%endwith %% CompiledModel

%endif %% _RLOPTIONS_

%% [EOF] xpcoptions.tlc
