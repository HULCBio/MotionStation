%%****************************************************************************
%%****************************************************************************
%%
%%              TLC for Encoder Input block from Real-Time Windows Target
%%
%%              $Revision: 1.2 $
%%              $Date: 2002/04/14 18:54:19 $
%%              $Author: batserve $
%%
%%              Copyright 1994-2002 The MathWorks, Inc.
%%
%%****************************************************************************
%%****************************************************************************


%implements "rtwinei" "C"


%% include RTWin library

%include "rtwinlib.tlc"



%% Function: BlockInstanceSetup ====================================================
%% Abstract:
%%      Register board to board table
%%

%function BlockInstanceSetup(block, system) void

%<RTWinRegisterBoard(block)>

%endfunction %% BlockInstanceSetup



%% Function: BlockInstanceData ====================================================
%% Abstract:
%%      Emit boards table into the code
%%
%function BlockInstanceData(block, system) void

%<RTWinEmitBoards()>

%endfunction %% BlockInstanceData



%% Function: InitializeConditions =============================
%% Abstract:
%%	Reset counters by calling:
%%      RTBIO_DriverIO(adapter, ENCODERINPUT, IORESET, n, ch, NULL, NULL);
%%
%function InitializeConditions(block, system) Output
%%
  /* %<Type> Block: %<Name> */
  {
    %assign n = LibBlockOutputSignalWidth(0)
    %assign chanptr = LibBlockParameterAddr(Channels, "", "", 0)
    %assign inputfilter = LibBlockParameter(InputFilter, "", "", 0)
%%
    ENCODERINPARM parm;

    parm.quad = %<QuadMode>;
    parm.index = %<IndexPulse>;
    parm.infilter =  %<inputfilter>;
    RTBIO_DriverIO(%<block.RTWinBoardIdx>, ENCODERINPUT, IORESET, %<n>, %<chanptr>, NULL, &parm);
  }
        
%endfunction %% InitializeConditions



%% Function: Outputs ===========================================================
%% Abstract:
%%	Read real-time encoder inputs by calling:
%%      RTBIO_DriverIO(adapter, ENCODERINPUT, IOREAD, n, ch, val, parm);
%%
%function Outputs(block, system) Output
%%
  /* %<Type> Block: %<Name> */
  {
    %assign n = LibBlockOutputSignalWidth(0)
    %assign chanptr = LibBlockParameterAddr(Channels, "", "", 0)
    %assign valptr = LibBlockOutputSignalAddr(0, "", "", 0)
    %assign inputfilter = LibBlockParameter(InputFilter, "", "", 0)
%%
    ENCODERINPARM parm;

    parm.quad = %<QuadMode>;
    parm.index = %<IndexPulse>;
    parm.infilter =  %<inputfilter>;
    RTBIO_DriverIO(%<block.RTWinBoardIdx>, ENCODERINPUT, IOREAD, %<n>, %<chanptr>, %<valptr>, &parm);
  }

%%
%endfunction %% Outputs



%% [EOF] rtwinei.tlc
