%%****************************************************************************
%%****************************************************************************
%%
%%              TLC for Other Output block from Real-Time Windows Target
%%
%%              $Revision: 1.8 $
%%              $Date: 2002/04/14 18:53:59 $
%%              $Author: batserve $
%%
%%              Copyright 1994-2002 The MathWorks, Inc.
%%
%%****************************************************************************
%%****************************************************************************


%implements "rtwinoo" "C"

%% include RTWin library

%include "rtwinlib.tlc"



%% Function: RTWinOtherOutput ====================================================
%% Abstract:
%%	Write real-time digital outputs by calling:
%%      RTBIO_DriverIO(adapter, DIGITALOUTPUT, IOWRITE, n, ch, val, parm);
%%
%function RTWinOtherOutput(block, srcval, valsize) Output
%%
    %assign n = LibBlockParameterSize(Channels)
    %assign n = n[0]*n[1]
    %assign chanptr = LibBlockParameterAddr(Channels, "", "", 0)
    %assign parmn = LibBlockParameterSize(OptParam)
    %assign parm = LibBlockParameterAddr(OptParam, "", "", 0)
%%
    {
      OTHERIOPARM parm;

      %assign valptr = RTWinExpandScalar(block, srcval, valsize, n)      %% optionally expand scalar value

      parm.n = %<parmn[1]>;
      parm.parm = %<parm>;
      RTBIO_DriverIO(%<block.RTWinBoardIdx>, OTHEROUTPUT, IOWRITE, %<n>, %<chanptr>, %<valptr>, &parm);
    }
%%
%endfunction %% RTWinOtherOutput



%% Function: BlockInstanceSetup ====================================================
%% Abstract:
%%      Register board to board table
%%

%function BlockInstanceSetup(block, system) void

%<RTWinRegisterBoard(block)>

%endfunction %% BlockInstanceSetup



%% Function: BlockInstanceData ====================================================
%% Abstract:
%%      Emit boards table into the code
%%
%function BlockInstanceData(block, system) void

%<RTWinEmitBoards()>

%endfunction %% BlockInstanceData



%% Function: Start ==============================================================
%% Abstract:
%%	Write initial value
%%
%function Start(block, system) Output
%%
  /* %<Type> Block: %<Name> */
  %assign valsize = LibBlockParameterSize(InitialValue)
  %assign valsize = valsize[0]*valsize[1]

  %if valsize==0

  /* no initial value should be set */

  %else
  {
    %assign srcval = LibBlockParameterAddr(InitialValue, "", "", 0)
    %<RTWinOtherOutput(block, srcval, valsize)>
  }
  %endif
%%
%endfunction %% Start



%% Function: Outputs ===========================================================
%% Abstract:
%%	Write real-time other outputs by calling:
%%      RTBIO_DriverIO(adapter, OTHERLOUTPUT, n, ch, val, parm);
%%
%function Outputs(block, system) Output
%%
  /* %<Type> Block: %<Name> */
  {
    %assign valsize = LibBlockInputSignalWidth(0)
    %assign srcval = LibBlockInputSignalAddr(0, "", "", 0)
    %<RTWinOtherOutput(block, srcval, valsize)>
  }

%%
%endfunction %% Outputs



%% Function: Terminate ==============================================================
%% Abstract:
%%	Write final value
%%
%function Terminate(block, system) Output
%%
  /* %<Type> Block: %<Name> */
  %assign valsize = LibBlockParameterSize(FinalValue)
  %assign valsize = valsize[0]*valsize[1]

  %if valsize==0

  /* no final value should be set */

  %else
  {
    %assign srcval = LibBlockParameterAddr(FinalValue, "", "", 0)
    %<RTWinOtherOutput(block, srcval, valsize)>
  }
  %endif
%%
%endfunction %% Terminate



%% [EOF] rtwinoo.tlc
