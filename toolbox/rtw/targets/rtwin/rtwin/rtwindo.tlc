%%****************************************************************************
%%****************************************************************************
%%
%%              TLC for Digital Output block from Real-Time Windows Target
%%
%%              $Revision: 1.8 $
%%              $Date: 2002/04/14 18:53:53 $
%%              $Author: batserve $
%%
%%              Copyright 1994-2002 The MathWorks, Inc.
%%
%%****************************************************************************
%%****************************************************************************


%implements "rtwindo" "C"

%% include RTWin library

%include "rtwinlib.tlc"



%% Function: RTWinDigitalOutput ====================================================
%% Abstract:
%%	Write real-time digital outputs by calling:
%%      RTBIO_DriverIO(adapter, DIGITALOUTPUT, IOWRITE, n, ch, val, parm);
%%
%function RTWinDigitalOutput(block, srcval, valsize) Output
%%
    %assign n = LibBlockParameterSize(Channels)
    %assign n = n[0]*n[1]
    %assign chanptr = LibBlockParameterAddr(Channels, "", "", 0)
    %assign bitmode = LibBlockParameterAddr(BitMode, "", "", 0)
%%
    {
      %assign valptr = RTWinExpandScalar(block, srcval, valsize, n)      %% optionally expand scalar value

      RTBIO_DriverIO(%<block.RTWinBoardIdx>, DIGITALOUTPUT, IOWRITE, %<n>, %<chanptr>, %<valptr>, %<bitmode>);
    }
%%
%endfunction %% RTWinDigitalOutput



%% Function: BlockInstanceSetup ====================================================
%% Abstract:
%%      Register board to board table
%%

%function BlockInstanceSetup(block, system) void

%<RTWinRegisterBoard(block)>

%endfunction %% BlockInstanceSetup



%% Function: BlockInstanceData ====================================================
%% Abstract:
%%      Emit boards table into the code
%%
%function BlockInstanceData(block, system) void

%<RTWinEmitBoards()>

%endfunction %% BlockInstanceData



%% Function: Start ==============================================================
%% Abstract:
%%	Write initial value
%%
%function Start(block, system) Output
%%
  /* %<Type> Block: %<Name> */
  %assign valsize = LibBlockParameterSize(InitialValue)
  %assign valsize = valsize[0]*valsize[1]

  %if valsize==0

  /* no initial value should be set */

  %else
  {
    %assign srcval = LibBlockParameterAddr(InitialValue, "", "", 0)
    %<RTWinDigitalOutput(block, srcval, valsize)>
  }
  %endif
%%
%endfunction %% Start



%% Function: Outputs ===========================================================
%% Abstract:
%%	Write outputs on each time step
%%
%function Outputs(block, system) Output
%%
  /* %<Type> Block: %<Name> */
  {
    %assign valsize = LibBlockInputSignalWidth(0)
    %assign srcval = LibBlockInputSignalAddr(0, "", "", 0)
    %<RTWinDigitalOutput(block, srcval, valsize)>
  }

%%
%endfunction %% Outputs



%% Function: Terminate ==============================================================
%% Abstract:
%%	Write final value
%%
%function Terminate(block, system) Output
%%
  /* %<Type> Block: %<Name> */
  %assign valsize = LibBlockParameterSize(FinalValue)
  %assign valsize = valsize[0]*valsize[1]

  %if valsize==0

  /* no final value should be set */

  %else
  {
    %assign srcval = LibBlockParameterAddr(FinalValue, "", "", 0)
    %<RTWinDigitalOutput(block, srcval, valsize)>
  }
  %endif
%%
%endfunction %% Terminate



%% [EOF] rtwindo.tlc
