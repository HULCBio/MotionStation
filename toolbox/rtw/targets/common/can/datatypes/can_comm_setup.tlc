%% File: can_comm_setup.tlc
%%
%% Abstract:
%%   
%%
%% $Revision: 1.14.4.1 $
%% $Date: 2004/04/19 01:20:23 $
%%
%% Copyright 2001-2002 The MathWorks, Inc.

%%-------------------------------------
%% Add the names that will be generated
%% in C for the CAN data types
%% ------------------------------------
%function SetupCanDataTypes(block) void
    %if !EXISTS(::have_registered_can_datatypes)
        %assign ::have_registered_can_datatypes = TLC_TRUE
        %with CompiledModel.DataTypes
          %foreach dtIdx = NumDataTypes
            %assign dt = DataType[dtIdx]
            %if (ISEQUAL(dt.Name,"CAN_MESSAGE_STANDARD") || ISEQUAL(dt.Name,"CAN_MESSAGE_EXTENDED"))
    
                %<SLibSetDataTypeName(dt, "CAN_FRAME")>
                %<SLibSetDataTypeGroundName(dt,"CAN_FRAME")>
                %<SLibSetDataTypeGroundValue(dt,"CAN_FRAME_GROUND")>
            %endif
          %endforeach
        %endwith
        %% Make a ground reference for CAN_FRAME
        %openfile buffer
        #include "can_msg.h"
        %closefile buffer
        %<LibExportFileCustomCode(buffer,"header")>

        %openfile buffer = "can_frame_ground.c"
            #include "can_msg.h"
            /* A ground reference for CAN_FRAME */
            CAN_FRAME CAN_FRAME_GROUND;
        %closefile buffer
        %<LibAddToModelSources("can_frame_ground")>
        %<LibCacheExtern("extern CAN_FRAME CAN_FRAME_GROUND;")>

        %% add in required includes to the main header file
        %<LibAddToCommonIncludes("can_msg.h")>
        %<LibAddToCommonIncludes("<string.h>")>
    %endif
%endfunction

/%
 SfcnCanFrameOutput {
	InitialValue {
	  LENGTH		  8U
	  RTR			  0U
	  type			  1U
	  ID			  78U
	  DATA			  [235U, 0U, 42U, 255U, 0U, 0U, 0U, 0U]
	}
%/

%function CAN_BlockInitialValues(block, system, canBlock) void
    %assign ret = ""
    %with SfcnCanFrameOutput.InitialValue
        %assign ret =  "{ %<LENGTH>, %<RTR>, %<type>, %<ID>, { \\\n" 
        %foreach idx = 8
            %if idx == 0
                %assign ret = ret + "%<DATA[idx]> " 
            %else
                %assign ret = ret + ", %<DATA[idx]> " 
            %endif
        %endforeach
        %assign ret = ret + " }\\\n }"
    %endwith
    %assign ret = [ "%<ret>" ]
    %return ret
%endfunction
