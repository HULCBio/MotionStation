%% $Revision: 1.1.6.2 $ 
%% $Date: 2004/02/06 00:31:38 $
%% Copyright 2001-2004 The MathWorks, Inc.
%% part of the Target for TI C6000 DSP


%% The following TLC code is common to the GRT-based and ERT-based
%% TI C6000 System Target files.  
%% This TLC file is included by both ti_c6000.tlc and ti_c6000_ert.tlc.

%if (BoardType=="Custom_C6416")
    %assign BoardType="C6416DSK"
%elseif (BoardType=="Custom_C6701")
    %assign BoardType="C6701EVM"
%elseif (BoardType=="Custom_C6711")
    %assign BoardType="C6711DSK"
%elseif (BoardType=="Custom_C6713")
    %assign BoardType="C6713DSK"
%endif

%% Set the size of the physical stack
%if LinkerCommandFile == "Internal_memory_map"
    %assign STACKSIZE = 8192
%else
    %assign STACKSIZE = 262144  
%endif

%% Set the maximum size of the local stack used by the RTW 
%assign MaxStackSize = STACKSIZE
%assign DivideStackByRate = TLC_TRUE

%% Force Use of Memory Map 1 (for now)
%assign C6x_Memory_Map = "map1"
%% Force Far Memory Model (for now)
%assign MemoryModel = "far"

%% Alignment specification:  8 bytes 
%assign PreStructDeclarePragma = "#pragma DATA_ALIGN(\%<DECLARE_NAME>,8)"

%% add include path to DSP Blockset tlc
%%
%assign matlab_path = FEVAL("matlabroot")
%assign dsp_tlc_path = matlab_path + "/toolbox/dspblks/dspmex/tlc_c"
%addincludepath dsp_tlc_path

%if DivideStackByRate
  %assign DivideStackByRate = TLC_FALSE
  %% Obtain the number of the sample times used in the model
  %assign NUMRATES = %<CompiledModel.NumSampleTimes>
  %assign MaxStackSize = STACKSIZE / NUMRATES
%endif

%include "utillib.tlc"
%if LibIsContinuous(0)
    %assign errTxt = "\n\nThe Embedded-C code format does not support " ...
      "continuous sample time blocks.  "
    %<LibReportError(errTxt)>
%endif

%include "c6000_interrupts.tlc"
%assign IRInfo = getInterruptInfo()

%include "c6000_validateRTWOptions.tlc"
%if (CompiledModel.ConfigSet.IsERTTarget == 0)   %% GRT-based target
    %include "c6000_main.tlc"
%endif
%include "c6000_bld.tlc"
%include "c6000_cmd.tlc"
%if BoardType=="C6701EVM"
    %include "c6701_csl.tlc"
%else
    %include "c6x1x_csl.tlc"
%endif
%if !useDSPBIOS
    %include "c6000_vectors.tlc"
%endif

%%register C62-specific TLC functions (need to take place before codegenentry)
%if (FPopt) && (CompiledModel.ConfigSet.ProdEndianess=="LittleEndian") 
    %if FILE_EXISTS("c62xTLCvtable.tlc")
        %include "c62xTLCvtable.tlc"
    %endif
%endif

%include "codegenentry.tlc"

%%get the names of the RTW-generated files (need to take place after codegenentry) 
%include "c6000_sourcefiles.tlc"

%if useDSPBIOS
    %include "c6000_cdb.tlc"
%endif

%% EOF  ti_c6000_stf_common.tlc
