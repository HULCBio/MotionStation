%% $RCSfile: stic6x_minmaxvalidx.tlc,v $
%% $Revision: 1.1.6.1 $
%% $Date: 2004/01/22 18:32:44 $
%%
%% Copyright 2001-2003 The MathWorks, Inc.
%%
%% Abstract: Compute minimum value (MINVAL), maximum value (MAXVAL)
%%           or index of maximum element (MAXIDX) of signed 16-bit integer
%%           input signal vectors using optimized TI C62/C64 DSPLIB fcn calls.

%implements stic6x_minmaxvalidx "C"

%include "tiblksutil.tlc"

%% Function: BlockTypeSetup ==============================================
%function BlockTypeSetup(block, system) void

  %<Include_ti_header(block, "dsp_" + getMinMaxFcnNameString())>

%endfunction %% BlockTypeSetup

%% Function: Outputs ==========================================================
%%
%function Outputs(block, system) Output
  %%
  %assign INPORT           = 0
  %assign OUTPORT          = 0
  %assign nChans           = LibBlockOutputSignalWidth(OUTPORT)
  %assign nSamps           = LibBlockInputSignalWidth(INPORT) / nChans
  %assign minmaxFcnNameStr = "DSP_" + getMinMaxFcnNameString()
  %%
  /* C%<chipID.Value[0]>x DSP Library %<minmaxFcnNameStr> (%<ParamSettings.FunctionName>) - '%<Name>' */
  %if (nChans == 1)
    %<LibBlockOutputSignal(OUTPORT,"","",0)> = %<minmaxFcnNameStr>( %<LibBlockInputSignalAddr(INPORT,"","",0)>, %<nSamps> );
  %else
    %assign outPortDataTypeStr = LibBlockOutputSignalDataTypeName(OUTPORT, "%<tRealPart>")
    {
      short *x = %<LibBlockInputSignalAddr(INPORT,"","",0)>;
      %<outPortDataTypeStr> *y = %<LibBlockOutputSignalAddr(OUTPORT,"","",0)>;
      int nChans = %<nChans>;

      while (nChans--) {
        *y++ = %<minmaxFcnNameStr>(x, %<nSamps>);
        x += %<nSamps>;
      }
    }
  %endif

%endfunction

%function getMinMaxFcnNameString() void
  %%
  %assign algType = CAST("Number",SFcnParamSettings.MINMAX_ALG_TYPE[0])
  %%
  %if (algType == 0)
    %assign minmaxFcnNameString = "minval"
  %elseif (algType == 1)
    %assign minmaxFcnNameString = "maxval"
  %else
    %assign minmaxFcnNameString = "maxidx"
  %endif
  %%
  %return minmaxFcnNameString
  %%
%endfunction %% getMinMaxFcnNameString

%% [EOF] stic6x_minmaxvalidx.tlc
