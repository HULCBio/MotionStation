%% $RCSfile: stic6x_radix2.tlc,v $
%% $Revision: 1.1.6.1 $
%% $Date: 2004/01/22 18:32:50 $
%%
%% Copyright 2001-2003 The MathWorks, Inc.
%%
%% Abstract: Compute radix-2 DIF forward FFT of a complex input 
%%           signal using optimized TI C62/C64 DSPLIB radix2 fcn call

%implements stic6x_radix2 "C"

%include "tiblksutil.tlc"

%% Function: BlockTypeSetup ==============================================
%function BlockTypeSetup(block, system) void

  %<Include_ti_header(block, "dsp_radix2")>
  %<Include_ti_header(block, "dsp_blk_move")>

%endfunction %% BlockTypeSetup

%% Function: Outputs ==========================================================
%%
%function Outputs(block, system) Output
  %%
  %assign INPORT        = 0
  %assign OUTPORT       = 0
  %assign numChans      = getInportChans(INPORT)
  %assign numSamps      = getInportSamps(INPORT)
  %assign inputAddress  = "(short *)" + LibBlockInputSignalAddr(INPORT, "", "", 0)
  %assign outputAddress = "(short *)" + LibBlockOutputSignalAddr(OUTPORT, "", "", 0)
  %assign W_RTP_Address = "(short *)" + LibBlockParameterAddr(W_RTP, "", "", 0)
  /* C%<chipID.Value[0]>x DSP Library radix2 (%<ParamSettings.FunctionName>) - '%<Name>' */
  %if (LibBlockInputSignalBufferDstPort(INPORT) != OUTPORT)
    DSP_blk_move(%<inputAddress>, %<outputAddress>, %<2 * numChans * numSamps>);
  %endif
  %if (numChans == 1)
    DSP_radix2(%<numSamps>, %<outputAddress>, %<W_RTP_Address>);
  %else
    {
      short    *outPtr           = %<outputAddress>;
      int       chanCount        = %<numChans>;
      const int realElemsPerChan = %<2 * numSamps>;
      while (chanCount--) {
        DSP_radix2(%<numSamps>, outPtr, %<W_RTP_Address>);
        outPtr += realElemsPerChan;
      }
    }
  %endif

%endfunction

%% [EOF] stic6x_radix2.tlc
