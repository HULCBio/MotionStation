%% $RCSfile: stic6x_fir_sym.tlc,v $
%% $Revision: 1.1.6.1 $
%% $Date: 2004/01/22 18:32:36 $
%%
%% Copyright 2001-2003 The MathWorks, Inc.
%%
%% Abstract: Implement symmetric real FIR filter using optimized TI C62/C64 DSPLIB FIR_SYM .asm code
%%
%%

%implements stic6x_fir_sym "C"

%include "tiblksutil.tlc"

%% Function: BlockTypeSetup ==============================================
%function BlockTypeSetup(block, system) void

  %<Include_ti_header(block, "dsp_fir_sym")>

%endfunction %% BlockTypeSetup


%% Function: BlockInstanceSetup ==================================================
%%
%function BlockInstanceSetup(block, system) void
  %assign INPORT    = 0
  %assign COEFFPORT = 1
  %assign OUTPORT   = 0
  %assign inDims    = LibBlockInputSignalDimensions(INPORT)
  %assign inFrame   = LibBlockInputSignalIsFrameData(INPORT)
  %assign numChans  = (inFrame) ? inDims[1] : LibBlockInputSignalWidth(INPORT)
  %assign nx        = (inFrame) ? inDims[0] : 1
  %assign block = block + INPORT + COEFFPORT + OUTPORT + numChans + nx
%endfunction %%BlockInstanceSetup


%% Function: InitializeConditions ==============================================
%%
%function InitializeConditions(block, system) Output
/* C%<chipID.Value[0]>x DSP Library (%<ParamSettings.FunctionName>) - '%<Name>' */
{
  %if (SFcnParamSettings.COEFFS_ARE_FROM_MASK)
    %% Coeffs are from mask - stored in RTP always
    %assign hSize   = LibBlockParameterSize(COEFFRTP)
    %assign nh      = (hSize[0] * hSize[1])
  %else
    %% Coeffs are from ports: determine where and how many
    %assign nh      = getInportSamps(COEFFPORT)
  %endif
  %assign delaysPerChan = nh - 1
  %assign inputsPerChan = nx
  %assign statesPerChan = delaysPerChan + inputsPerChan
  %assign ICSize        = LibBlockParameterSize(ICRTP)
  %assign numICs        = (ICSize[0] * ICSize[1])
  short *statePtr = %<LibBlockDWorkAddr(FILTBUFF, "", "", 0)>;
  %if (numICs > 1)
  const short *ICPtr = %<LibBlockParameterAddr(ICRTP, "", "", 0)>;
  %endif
  %if (numChans > 1)
    int chanCount = %<numChans>;
    while (chanCount--) { %% Begin channel loop if needed
  %endif
  %if (numICs == 1)
    %% Scalar IC
      int delayCount = %<delaysPerChan>;
      while (delayCount--) {
        *statePtr++ = %<LibBlockParameter(ICRTP, "", "", 0)>;
      }
  %else
    memcpy(statePtr, ICPtr, %<2 * delaysPerChan>);
    %if (numICs > delaysPerChan)
      %% multi-channel case, different vector of ICs per channel, 
      %% so bump IC pointer in channel loop
      ICPtr += %<delaysPerChan>;
    %endif
  %endif
  %if (numChans > 1)
      statePtr += %<statesPerChan>;
    }  %% End channel loop if needed
  %endif
}

%endfunction %% InitializeConditions
  

%% Function: Outputs ==========================================================
%%
%function Outputs(block, system) Output
  %%
  %if (SFcnParamSettings.COEFFS_ARE_FROM_MASK)
    %% Coeffs are from mask - stored in RTP always
    %assign hPtr    = LibBlockParameterAddr(COEFFRTP, "", "", 0)
    %assign hSize   = LibBlockParameterSize(COEFFRTP)
    %assign nh      = (hSize[0] * hSize[1])
  %else
    %% Coeffs are from ports: determine where and how many
    %assign hPtr    = LibBlockInputSignalAddr(COEFFPORT, "", "", 0)
    %assign nh      = getInportSamps(COEFFPORT)
  %endif
  %assign nf        = nh - 1
  %assign nhTI      = nh / 2
  %assign uPtr      = LibBlockInputSignalAddr(INPORT, "", "", 0)
  %assign rPtr      = LibBlockOutputSignalAddr(OUTPORT, "", "", 0)
  %assign fPtr      = LibBlockDWorkAddr(FILTBUFF, "", "", 0)
  /* C%<chipID.Value[0]>x DSP Library (%<ParamSettings.FunctionName>) - '%<Name>' */
  %if (numChans == 1)
    %assign x1Ptr = LibBlockDWorkAddr(FILTBUFF, "", "", nf)
    %assign x2Ptr = LibBlockDWorkAddr(FILTBUFF, "", "", nx)
    /* Copy current inputs to filter buffer position x = f[nf] */
    memcpy(%<x1Ptr>, %<uPtr>, %<2*nx>);
    
    /* Run FIR_SYM filter algorithm on filter buffer f[] */
    DSP_fir_sym(%<fPtr>, %<hPtr>, %<rPtr>, %<nhTI>, %<nx>, %<SFcnParamSettings.OUT_SHIFT>);
    
    /* Update filter states: move previous nf inputs from f[nx] to f[0] */
    memmove(%<fPtr>, %<x2Ptr>, %<2*nf>);
    
  %else
    {
      const short *u = %<uPtr>;
      const short *h = %<hPtr>;
      short *r       = (short *) %<rPtr>;
      short *f       = (short *) %<fPtr>;
      short *x;
      int    chanCount = %<numChans>;
      
      while (chanCount--) {
        /* Copy current inputs to filter buffer position x = f[nf] */
        x = f + %<nf>;
        memcpy(x, u, %<2*nx>);
        u += %<nx>;
        
        /* Run FIR_SYM filter algorithm on filter buffer f[] */
        %% h should be const but fir_<blah>() argument list does not conform to const rules
        DSP_fir_sym(f, (short *)h, r, %<nhTI>, %<nx>, %<SFcnParamSettings.OUT_SHIFT>);
        r += %<nx>;
        
        /* Update filter states: move previous nf inputs from f[nx] to f[0] */
        x = f + %<nx>;
        memmove(f, x, %<2*nf>);
        f += %<nx + nf>;
      }
    }
  %endif

%endfunction

%% [EOF] stic6x_fir_sym.tlc
