%% $RCSfile: stic6x_bitrev_cplx.tlc,v $
%% $Revision: 1.1.6.1 $
%% $Date: 2004/01/22 18:32:31 $
%%
%% Copyright 2001-2003 The MathWorks, Inc.
%%
%% Abstract: Bit/digit reverse elements of a complex input vector
%%           using optimized TI C62/64 DSPLIB bitrev_cplx fcn call

%implements stic6x_bitrev_cplx "C"

%include "tiblksutil.tlc"

%% Function: BlockTypeSetup ==============================================
%function BlockTypeSetup(block, system) void

  %<Include_ti_header(block, "dsp_bitrev_cplx")>

%endfunction %% BlockTypeSetup

%% Function: Outputs ==========================================================
%%
%function Outputs(block, system) Output
  %%
  %assign INPORT        = 0
  %assign OUTPORT       = 0
  %assign numChans      = getInportChans(INPORT)
  %assign numSamps      = getInportSamps(INPORT)
  %% static void bitrev_cplx(int *x, short *index, int nx)
  %assign signalAddress  = "(int *)" + LibBlockOutputSignalAddr(OUTPORT, "", "", 0)
  %assign IDXTBL_Address = "(short *)" + LibBlockParameterAddr(IDXTBL_RTP, "", "", 0)
  /* C%<chipID.Value[0]>x DSP Library bitrev_cplx (%<ParamSettings.FunctionName>) - '%<Name>' */
  %if (LibBlockInputSignalBufferDstPort(INPORT) != OUTPORT)
    %<LibBlockCopyInputsToOutputs(INPORT, OUTPORT)>
  %endif
  %if (numChans == 1)
    DSP_bitrev_cplx(%<signalAddress>, %<IDXTBL_Address>, %<numSamps>);
  %else
    {
      int      *inPtr            = %<signalAddress>;
      int       chanCount        = %<numChans>;
      const int realElemsPerChan = %<2 * numSamps>;
      while (chanCount--) {
        DSP_bitrev_cplx(inPtr, %<IDXTBL_Address>, %<numSamps>);
        inPtr += realElemsPerChan;
      }
    }
  %endif

%endfunction

%% [EOF] stic6x_bitrev_cplx.tlc
