'CPM Printer motor program
'-----------------------

SWITCH a
   CASE 123		'Homing Sequence
      GOSUB0	'go to home subroutine
      BREAK
   CASE 124		'Move with Print
      GOSUB1	'go to subroutine #1
      BREAK
   DEFAULT		'Respond with initialized message
      BAUD38400
	PRINT("Printer motor initialized",#13)
      BREAK
ENDS		'End Homing Sequence
UDM		'Return input D to limit switch operation
UCP		'Return input C to limit switch operation
a=0		'Reassign mode variable a to NULL
b=0		'Reassign trigger output position to zero
'------------------------------------------------------------------
END	'END OF PROGRAM

'---------------------------------------------------------
C0   'HOME ROUTINE

UDI	'Set port D as an input port
UCI	'Set port C as an input port

r=1		'Home direction (Make r a -1 to reverse home direction)
v=40000	'Home speed
MV		'Enable velocity mode
A=1000	'Home ACCEL

'///// IF axis already on limit, then drive off until switch open,
'		then drive again until past the next index pulse
IF UCI==0	'IF axis already on limit
   PRINT("Moving off of home switch",#13)
   V=v*r		'Home speed corrected for direction
   G			'start a velocity mode move
   WHILE UCI==0	'Wait for switch to open 
   LOOP		'When input port goes high, it drops out of the loop.
   i=I		'Assigning the index marker to i
   WHILE UCI==i	'Wait for next index pulse
   LOOP		'When index pulse location changes, it drops out of the loop.
   X			'Slow motor motion to a stop
ELSE
   PRINT("Axis not on home switch",#13)
ENDIF

'///// Homing operation
PRINT("Homing motor",#13)
 V=-v*r	'Home speed corrected for direction
 G		'start a velocity mode move
 i=I		'Assigning the index marker to i
	'Drive off of limit until switch opens
 WHILE UCI==1 	
  i=I		'Assigning the index marker to i
 LOOP
'When input port gets pulled to ground it drops out of the loop.
 X		'Slow motor motion to a stop
 PRINT("Home switch detected",#13)
 P=i		'Set position target to the last index
 PRINT("Moving to Index Pulse",#13)
 G
 TWAIT
 WAIT=1000	
 O=0		'Setting present position to HOME
 PRINT("Motor is at Home",#13)

RETURN	'C0 HOME ROUTINE

'---------------------------------------------------------
C1   'Move With Print Routine (Fire output A when position is greater than b

UAO		'Set digital channel A to output
c=0		'initialize exit loop condition variable
G		'Initiate Move
WHILE c==0
	IF @P>b 
		c=1
	ENDIF
	IF @PE<=0 
		c=1
	ENDIF
LOOP
UA=1		'Set output channel A to TRUE
 PRINT("Trigger Set",#13)

RETURN	'C1 Move With Print Routine



