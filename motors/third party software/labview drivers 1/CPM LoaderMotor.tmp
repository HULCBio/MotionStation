'CPM Loader motor program
'-----------------------

SWITCH a
   CASE 123		'Homing Sequence
      GOSUB0	'go to home subroutine
      BREAK
   CASE 124		'Read Tag Present Sensor
      GOSUB1	'go to subroutine 1
      BREAK
   CASE 125		'Drive UP until tag @ TOP
      GOSUB2	'go to subroutine 2
      BREAK
   CASE 126		'Drive DOWN until tag !@ TOP
      GOSUB3	'go to subroutine 3
      BREAK
   DEFAULT		'Respond with initialized message
      BAUD38400
      PRINT("Loader motor initialized",#13)
      BREAK
ENDS		'End Homing Sequence
UDM		'Return input D to limit switch operation
UCP		'Return input C to limit switch operation
MP		'Enable position mode
a=0		'Reassign mode variable a to NULL
'------------------------------------------------------------------
END	'END OF PROGRAM

'---------------------------------------------------------
C0   'HOME ROUTINE

UCI	'Set port C as an input port
UDI	'Set port D as an input port

r=1		'Home direction (Make r a -1 to reverse home direction)
v=100000	'Home speed
MV		'Enable velocity mode
A=1000	'Home ACCEL

'///// Find home switch
PRINT("Finding home switch",#13)
 V=-v*r	'Home speed corrected for direction
 G	'start a velocity mode move
'The following line looks at Port D as a TTL level Input.
 WHILE UDI==1 
 LOOP
 X		'Slow motor motion to a stop
'When Port D gets pulled to ground it drops out of the loop.
 PRINT("Hit Limit Switch on Port D",#13)

'///// Drive off home switch until switch open,
'		then drive again until past the next index pulse
IF UDI==0	'IF axis already on limit
   PRINT("Moving off of home switch",#13)
   V=v*r		'Home speed corrected for direction
   G			'start a velocity mode move
   WHILE UDI==0	'Wait for switch to open 
   LOOP		'When Port D goes high, it drops out of the loop.
   i=I		'Assigning the index marker to i
   WHILE I==i	'Wait for next index pulse
   LOOP		'When index pulse location changes, it drops out of the loop.
   X			'Slow motor motion to a stop
ELSE
   PRINT("Axis not on home switch",#13)
ENDIF

'///// Homing operation drive to home switch while recording index


PRINT("Homing motor",#13)
 i=I	'Assigning the index marker to i
 V=-v*r	'Home speed corrected for direction
 G	'start a velocity mode move
'The following line looks at Port D as a TTL level Input.
 WHILE UDI==1 
  i=I	'Assigning the index marker to i
 LOOP
'When Port D gets pulled to ground it drops out of the loop.
 PRINT("Hit Limit Switch on Port D",#13)
 X		'Slow motor motion to a stop
 P=i
 PRINT("Moving to Index Pulse: ",i,#13)
 G
 TWAIT
 WAIT=1000	
 O=0		'Setting present position to HOME
 PRINT("Motor is at Home",#13)

RETURN	'End of Subroutine 0, Homing routine
'---------------------------------------------------------

'---------------------------------------------------------
C1   'Begin Subroutine 1; Read Tag@TOP Sensor
	UAI		'Set port A as an input port for tag sensor
      b=UAI		'Assign value of tag present sensor to variable b
	PRINT(b,#13)	'Return digital value
RETURN	'End of Subroutine 1, Read Tag@TOP Sensor
'---------------------------------------------------------

'---------------------------------------------------------
C2		'Begin Subroutine 2; Drive UP until tag @ TOP
	G	'Start a position mode move (assumes velocity & position were set)
 	WHILE UAI==0	'Monitor port A (Tag@TOP) for active state
 	LOOP
 	X	'Slow motor motion to a stop
	PRINT("Done",#13)	'Return message with line feed
RETURN	'End of Subroutine 2, Drive UP until tag @ TOP
'---------------------------------------------------------

'---------------------------------------------------------
C3		'Begin Subroutine 3; Drive DOWN until tag !@ TOP
	G	'Start a velocity mode move (assumes velocity has been set to a positive value))
 	WHILE UAI==1	'Monitor port A (Tag@TOP) for active state
 	LOOP
 	X	'Slow motor motion to a stop
	PRINT("Done",#13)	'Return message with line feed
RETURN	'End of Subroutine 3, Drive DOWN until tag !@ TOP
'---------------------------------------------------------



