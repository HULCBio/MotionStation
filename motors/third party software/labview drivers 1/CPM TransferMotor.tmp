'CPM Transfer motor program
'-----------------------

SWITCH a
   CASE 123		'Homing Sequence
      GOSUB0	'go to home subroutine
      BREAK
   DEFAULT		'Respond with initialized message
      BAUD38400
      PRINT("Transer motor initialized",#13)
      BREAK
ENDS		'End Homing Sequence
UDM		'Return input D to limit switch operation
UCP		'Return input C to limit switch operation
a=0		'Reassign mode variable a to NULL
'------------------------------------------------------------------
END	'END OF PROGRAM

'---------------------------------------------------------
C0   'HOME ROUTINE

UDI		'Set port D as an input port
UCI		'Set port C as an input port
KP=300	'Set Proportional tuning constant
KI=28		'Set Integral tuning contstant
KD=600	'Set Differential tuning constant
KL=20		'Set Integral limit
KS=1		'Set Differential sample rate
KV=300	'Set Velocity feedforward constant
KA=200	'Set Acceleration feedforward tuning constant
KG=0		'Set Gravitational tuning coefficient
E=2000	'Set Max Position Error

r=1		'Home direction (Make r a -1 to reverse home direction)
v=500000	'Home speed
MV		'Enable velocity mode
A=1000	'Home ACCEL

'///// IF axis already on limit, then drive off until switch open,
'		then drive again until past the next index pulse
IF UCI==1	'IF axis already on limit
   PRINT("Moving off of home switch",#13)
   V=10000*r	'Home speed corrected for direction
   G			'start a velocity mode move
   WHILE UCI==1	'Wait for switch to open 
   LOOP		'When Port D goes high, it drops out of the loop.
   i=I		'Assigning the index marker to i
   WHILE UCI==i	'Wait for next index pulse
   LOOP		'When index pulse location changes, it drops out of the loop.
   X			'Slow motor motion to a stop
ELSE
   PRINT("Axis not on home switch",#13)
ENDIF

'///// Homing operation
PRINT("Homing motor",#13)
 V=-40000*r	'Home speed corrected for direction
 G	'start a velocity mode move
 i=I	'Assigning the index marker to i
'The following line looks at Port D as a TTL level Input.
 WHILE UCI==0 
  i=I	'Assigning the index marker to i
 LOOP
'When Port C gets pulled to ground it drops out of the loop.
 PRINT("Hit Limit Switch on Port D",#13)
 X		'Slow motor motion to a stop
 P=i
 V=-10000*r	'Home speed corrected for direction
PRINT("Moving to Index Pulse",#13)
 G
 TWAIT
 WAIT=1000	
 O=0		'Setting present position to HOME
 PRINT("Motor is at Home",#13)

RETURN




