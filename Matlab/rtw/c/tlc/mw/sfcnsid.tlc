%% ============================================================================
%% $RCSfile: sfcnsid.tlc,v $
%% $Revision: 1.18.4.2 $ 
%% $Date: 2004/04/14 23:48:55 $
%%
%% Abstract:
%%   This system file creates model_sid.h for S-Function code format
%%
%% Copyright 1994-2003 The MathWorks, Inc.
%%
%selectfile NULL_FILE

%% ============================================================================
%% The S-Function file model_sid.h is created as follows:
%%
%% /* File banner */
%%
%% {
%%   #undef _RTW_HEADER_FILE_model_sf_
%%   #include "model_sf.h"
%%   
%%   extern model_Parameters model_DefaultParameters;
%%   
%%   {
%%     Necessary RTW declarations
%%     Declare accessed SimStruct variables
%%     
%%     Decalare and setup local SimStruct
%%     Non-finite assignments
%%     Register embedded S-Functions
%%     Block instance data
%%     {
%%       User declarations ..... LibMdlRegCustomCode(buffer, "declaration")
%%       User entry code ....... LibMdlRegCustomCode(buffer, "execution")
%%       User exit code ........ LibMdlRegCustomCode(buffer, "trailer")
%%     }
%%   }
%% }
%%
%% /* [EOF] model_sid.h */

%if EXISTS("_SFCNSID_") == 0
%assign _SFCNSID_ = 1

%realformat "CONCISE"

%assign opFile = SLibAddModelFile("SystemHeader","Simulink","%<OrigName>_sid")

%openfile bannerBuf
/*
* %<OrigName>_sid.h
*
%<SLibCommonHeaderInfo()>\
*
  %<SLibGenSourcesComment()>
*/
%closefile bannerBuf
%<SLibSetModelFileAttribute(opFile, "Banner", bannerBuf)>
%undef bannerBuf

%openfile fcnsBuf
/* statically allocated instance data for model: %<OrigName> */
{
  %% Parameter Tuning map
  %if ParameterTuning == 1
    
    #include "mdl_info.h"
    #include "%<Name>_pt.c"
  %endif
  %if !LibParametersStructIsEmpty()
    extern %<tParametersType> %<OrigName>_DefaultParameters;
  %endif
  %if !LibConstBlockIOStructIsEmpty()
    extern %<tConstBlockIOType> %<tInvariantSignals>;
  %endif
  {
    %<SIDLocalSDeclarations>\
    %<SIDLocalSRegistration>\
    %%
    %% Buffer up register code, and declare any accessed simstruct variables
    %%
    %openfile tmpBuffer
    %<LibDumpNonFiniteAssignmentCache()>\
    %<SIDSFunctionRegistration>\
    %<SIDBlockInstanceData>\
    %if !LibRegFcnUserDeclarationIsEmpty()
      %<"{">
      %<LibDumpRegFcnUserDeclarationCode()>
    %endif
    %<LibDumpRegFcnUserEntryCode()>\
    %<LibDumpRegFcnUserExitCode()>\
    %if !LibRegFcnUserDeclarationIsEmpty()
      %<"}">
    %endif
    %closefile tmpBuffer
    %if !WHITE_SPACE(tmpBuffer)
      {
	%<tmpBuffer>\
      }
    %endif
  }
}
%closefile fcnsBuf

%<SLibSetModelFileAttribute(opFile, "Functions", fcnsBuf)>

%with CompiledModel
  %undef SIDLocalSDeclarations
  %undef SIDLocalSRegistration
  %undef SIDSFunctionRegistration
  %undef SIDBlockInstanceData
%endwith
%undef tmpBuffer
%undef fcnsBuf

%selectfile NULL_FILE

%endif  %% _SFCNSID_

%% [EOF] sfcnsid.tlc
