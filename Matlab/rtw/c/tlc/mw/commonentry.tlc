%% ============================================================================
%% 
%% $Revision: 1.1.6.3 $ 
%% 
%%
%% Abstract:
%%
%%   This system file starts the CodeFormat specific code generation process.
%%
%% Copyright 1994-2003 The MathWorks, Inc.
%%
%selectfile NULL_FILE

%if EXISTS("_COMMONENTRY_") == 0
%assign _COMMONENTRY_ = 1

%% Functions for creating the external mode data type transition tables
%% from the global memory map.
%include "extmodemaplib.tlc"

%% ============================================================================
%% Start code generation process based on code format.
%%

%% RSIM needs external mode to generate the data type transition info.
%if EXISTS("_RSIM_") && !IsModelReferenceTarget() 
  %assign ExtMode = 1
%endif

%with CompiledModel
  %switch CodeFormat
    %case "RealTime"
    %case "Embedded-C"
      %include "formatwide.tlc"
      %assign mapfile = "formatmap"
      %break
    %case "RealTimeMalloc"
      %include "formatwide.tlc"
      %assign mapfile = "mrtmap"
      %break
    %case "S-Function"
      %include "formatwide.tlc"
      %break
    %default
      %assign errTxt = "Unknown CodeFormat: %<CodeFormat>"
      %<LibReportFatalError(errTxt)>
  %endswitch
  %%
  %% Generate the interface API if the data type transition table is needed.
  %%
  %if ExtMode == 1
    %assign ::CompiledModel.GenerateInterfaceAPI = 1
  %endif
  %%
  %% code generation is complete.  optionally generate interface API for
  %% data not of custom storage class
  %%
  %if GenerateInterfaceAPI
    %assign ::BlockFcn = "GlobalMap"
    %<SLibMapData()>
    %assign ::BlockFcn = "Unknown"
  %endif
  %%
  %% If custom and/or noncustom part of map was created, do things with the 
  %% created map
  %%
  %if InterfaceAPIGenerated || CustomInterfaceAPIGenerated
    
    %%
    %% Optionally create data type transition information to map signals
    %% and parameters
    %%
    %if ExtMode == 1
      %% Write map m-file for Simulink in External Mode
      %<SLibWriteExtModeHostMapFile()>
      %include "%<mapfile>.tlc"
    %endif
    %<SLibPostMapData()>
    
  %endif
  %% ==========================================================================
  %% Optionally create ASAP2 file
  %%
  %if GenerateASAP2 == 1
    %include "asap2main.tlc"
  %endif
  %% ==========================================================================
  %% Optionally create HTML report
  %%
  %if GenerateReport == 1 && CodeFormat != "S-Function"
    %include "report.tlc"
  %endif
%endwith

%endif  %% _COMMONENTRY_

%% [EOF] commonentry.tlc
