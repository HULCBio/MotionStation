%% ============================================================================
%% File: prm_assignbuffers.tlc
%%
%% Abstract:
%%   Assign parameter buffers into appropriate files.
%%   
%% $Revision: 1.1.6.5 $ 
%% $Date: 2004/04/14 23:48:35 $
%%
%% Copyright 1994-2004 The MathWorks, Inc.
%% 
%% ============================================================================
%selectfile NULL_FILE

%if EXISTS("_PRM_ASSIGNBUFFERS_") == 0
%assign _PRM_ASSIGNBUFFERS_ = 1

%include "prm_cacheutils.tlc"

%% Function: SLibAssignPrmBuffers =============================================
%% Abstract:
%%   Assign parameter buffers into appropriate files.
%%
%function SLibAssignPrmBuffers() void
  
  %if SLibAreSimulinkGlobalParamsUnstructured()
    %% FOR MODEL REFERENCE: SimulinkGlobal parameters treated like ExportedGlobal
    %assign declBuffer = SLibGetPrmBuffer("UnstructuredSimulinkGlobal", "Decl")
    %assign opFile = GetBaseFile("SystemHeader")
    %<SLibSetModelFileAttribute(opFile, "ExternData", declBuffer)>

  %elseif ModelParameters.NumInrtP > 0
    %assert(!IsModelReferenceTarget())
    %% Assign rtP structures to appropriate files for each system.
    %foreach sysIdx = NumSystems
      %assign sys = System[sysIdx]
      %if !SLibSystemPrmBufferIsEmpty(sys, "SimulinkGlobal", "Decl")
	%% Assign parameter structure declaration for each system
	%assign declBuffer = ...
	  SLibGetSystemPrmBuffer(sys, "SimulinkGlobal", "Decl")
	%assign fileName = SLibGetSystemOutputFileBaseName(sys)
	%assign opFile = SLibAddModelFile("SystemHeader", "Simulink", fileName)
	%<SLibSetModelFileAttribute(opFile, "Typedefs", declBuffer)>
	
	%% ASSERT: Typedef can never be empty if the Decl is not empty
	%assert !SLibSystemPrmBufferIsEmpty(sys, "SimulinkGlobal", "Typedef")
	
	%% Assign parameter structure typedef for each system
	%assign typedefBuffer = ...
	  SLibGetSystemPrmBuffer(sys, "SimulinkGlobal", "Typedef")
	%assign fileName = "%<CompiledModel.Name>_types"
	%assign opFile = SLibAddModelFile("SystemHeader", "Simulink", fileName)
	%<SLibSetModelFileAttribute(opFile, "Typedefs", typedefBuffer)>
      %endif
    %endforeach
  %endif
  
%endfunction %% SLibAssignPrmBuffers
  
%endif %% EXISTS("_PRM_ASSIGNBUFFERS_")

%% [EOF] prm_assignbuffers.tlc
