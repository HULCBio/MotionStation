%% ============================================================================
%% $RCSfile: formatrtm.tlc,v $
%% $Revision: 1.14.4.7 $ 
%% $Date: 2004/04/14 23:48:19 $
%%
%% Abstract:
%%   Generate headers files related to the rtModel.
%%
%% Copyright 1994-2004 The MathWorks, Inc.
%%
%selectfile NULL_FILE

%if EXISTS("_FORMATRTM_") == 0
%assign _FORMATRTM_ = 1

%% Function: SLibGenRTMIncludes ================================================
%% Abstract:
%%   Generate the includes needed for the rtModel
%%
%function SLibGenRTMIncludes() void
  %openfile tmpBuf
  %if GenRTModel
    #include <limits.h>
  %endif
  %closefile tmpBuf
  %return tmpBuf
%endfunction %% SLibGenRTMIncludes


%% Function: SLibGenRTMTypedefAndMacros ========================================
%% Abstract:
%%   Generate the typedef and macros and related declarations for the rtModel
%%
%function SLibGenRTMTypedefAndMacros() void
  %assign ret = ["", "", ""]  %% types, macros, declarations

  %openfile bufTypes
  %openfile bufMacros
  %openfile bufDecls
  %if GenRTModel
    %selectfile bufTypes
    
    %% Real-time Model Data Structure
    %<RTMGenRtMdlTypedef()>\

    %if !EmptyRealTimeObject
      %if (CodeFormat == "Embedded-C")
	%if !MultiInstanceERTCode  && !IsModelReferenceTarget()
          %selectfile bufDecls

	  /* Real-time Model object */
	  extern %<tSimStructType> *%<RTMGetModelSS()>;

	%endif
      %else
	%assign rtModelObjType = tSimStructType
        %selectfile bufDecls

	/* Simulation structure */
	extern %<rtModelObjType> *const %<RTMGetModelSS()>;

      %endif
    %endif
    %selectfile bufMacros
      
    /* Macros for accessing real-time model data structure  */
    %assign macroLocation = (CodeFormat == "Embedded-C") ? "PUBLIC" : ""
    %<RTMGenMacroDefines(macroLocation)>\
    %if (RTWCAPIStates==1) && MatFileLogging
      #define UseMMIDataLogging
    %endif
    %if CodeFormat == "Embedded-C" && ...
      !EmptyRealTimeObject
      %if !GenerateSampleERTMain && !PrefixModelToStructs
	%% Static ert main assume PrefixModelToStructs is true
	#define %<CompiledModel.Name>_M rtM
      %endif
    %endif
    %if (CodeFormat != "Embedded-C" || GenerateGRTWrapper) && GenRTModel
      /* Definition for use in the target main file */
      #define %<CompiledModel.Name>_rtModel %<tSimStructType>
    %endif
  %else %%For targets that don't use rtModel yet
    %assign rootSystem = System[NumSystems-1]
    %assign reqInsts   = LibGetSystemField(rootSystem, "ReqRootPrmHdrDataInsts")
    %if reqInsts.SimStructInst 
      %selectfile bufDecls
      
      /* Simulation Structure */
      extern %<rtModelObjType> *const %<RTMGetModelSS()>;

    %endif
  %endif
  %closefile bufDecls
  %closefile bufMacros
  %closefile bufTypes

  %assign ret[0] = bufTypes
  %assign ret[1] = bufMacros
  %assign ret[2] = bufDecls
  %return ret
%endfunction %% SLibGenRTMTypdefAndMacros


%% Function: SLibGenRTModelHFile =========================================
%% Abstract:
%%   Generate the rtmodel.h file.
%%
%function SLibGenRTModelHFile() void
  %if (CodeFormat != "Embedded-C" || GenerateGRTWrapper) && ...
    GenRTModel && !IsModelReferenceTarget()
    %assign rtmhFile = SLibAddModelFile("SystemHeader","Simulink",...
      "rtmodel")
    %openfile tmpBuf
    /*
    *  rtmodel.h:
    *
    %<SLibCommonHeaderInfo()>\
    */
    %closefile tmpBuf
    %<SLibSetModelFileAttribute(rtmhFile, "Banner", tmpBuf)>
    
    %openfile tmpBuf
    /*
    *  Includes the appropriate headers when we are using rtModel
    */
    #include "%<Name>.h"
    %closefile tmpBuf
    %<SLibSetModelFileAttribute(rtmhFile, "Includes", tmpBuf)>
  %endif
%endfunction %% SLibGenRTModelHFile
%endif  %% _FORMATRTM_

%% [EOF] formatrtm.tlc
