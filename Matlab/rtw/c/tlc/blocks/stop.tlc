%% 
%% $Revision: 1.1.6.2 $
%% 
%%
%% Copyright 1994-2003 The MathWorks, Inc.
%%
%% Abstract:
%%      Stop block target file. 
%%

%implements Stop "C"

%% Function: BlockInstanceSetup ==============================================
%% Abstract:
%%   Set expression folding compliance
%%
%function BlockInstanceSetup(block, system) void
  %<LibBlockSetIsExpressionCompliant(block)>
%endfunction

%% Function: Outputs ===========================================================
%% Abstract:
%%      If our input has become non-zero, then request a stop.
%%
%function Outputs(block, system) Output
  %assign dType = LibBlockInputSignalAliasedThruDataTypeId(0)
  %if (dType == tSS_DOUBLE)
    %assign compareOp = " != 0.0"
  %elseif (dType == tSS_BOOLEAN)
    %assign compareOp = ""
  %else
    %%START_ASSERT
    %assign dtName = LibGetDataTypeNameFromId(dType)
    %assign errTxt = "Invalid data type %<dtName>."
    %<LibBlockReportError(block, errTxt)>
    %%END_ASSERT
  %endif
  %assign compareScaler = ""
  %openfile tmpBuf
  %<RTMSetStopRequested(1)>;
  %closefile tmpBuf
  %assign rollVars = ["u0"]
  %roll sigIdx = RollRegions, lcv = RollThreshold, block, "Roller", rollVars
    %assign u = LibBlockInputSignal(0, "", lcv, sigIdx)
    %if lcv != ""
      if (%<u>%<compareOp>) {
      %<tmpBuf>\
      break;
      }
    %else
      %if compareScaler != ""
	%assign compareScaler = "%<compareScaler> || %<u>%<compareOp>"	  
      %else
	%assign compareScaler = "%<u>%<compareOp>"
      %endif
    %endif
  %endroll
  %if compareScaler != ""
    if (%<compareScaler>) {
    %<tmpBuf>\
    }
  %endif

%endfunction

%% [EOF] stop.tlc
