%% $RCSfile: MPTGetSet.tlc,v $
%% $Revision: 1.1.6.3 $
%%
%% Copyright 1994-2004 The MathWorks, Inc.
%% 
%selectfile NULL_FILE
%implements * "C"

%% GetSet is a custom storage class that is used to define signals
%% (including states) whose values are read and written to using 
%% "get" and "set" functions

%% Function: DataAccess ======================================================
%% Abstract:
%%   DataAccess provides an API for requesting code fragments or other
%%   information corresponding to data of this custom storage class.
%%
%function DataAccess(record, request, idx, reim, extra) void

  %switch request
 
    %case "initialize"
      %% Use the default initialization to ground.
      %return ""
      %break
      
    %case "contents"
      %% Use the default reference to a global variable.
      %assign props = LibGetCustomStorageAttributes(record)
      %if idx != ""
	%% get rid of the [] brackets
	%assign idx[0] = " "
	%assign length = SIZE(idx, 1)
	%assign idx[length-1] = " "
	%return "%<props.GetFunction>(%<idx>)"
      %else
	%return "%<props.GetFunction>()"
      %endif
      %break

    %case "address"
      %% Use the default address of a global variable.
      %return "address not supported for accessors"
      %break

    %case "declare"
      %% Since the data is supposed to be internal to the code
      %% generated, generate no declaration for external use.
      %return ""
      %break

    %case "define"
      %% Use the default definition of a global variable.
      %return ""
      %break
      
    %case "layout"
      %% Uses The Mathworks' data layout.
      %return ["other"]
      %break

    %case "qualifier"
      %% No type qualifier.
      %return ""
      %break
      
    %case "set"
      %assign props = LibGetCustomStorageAttributes(record)
      %if idx != ""
	%% get rid of the [] brackets
	%assign idx[0] = " "
	%assign length = SIZE(idx, 1)
	%assign idx[length-1] = " "
	%return "%<props.SetFunction>(%<idx>, %<extra>);\n"
      %else
	%return "%<props.SetFunction>(%<extra>);\n"
      %endif

    %default
      %% Unknown access type.
      %return LibDefaultCustomStorageUnknownDataAccessType ...
	(record, request, idx, reim)
      %break

  %endswitch

%endfunction



%% Function: ClassAccess ============================================
%% Abstract:
%%   ClassAccess provides an API for requesting information or action
%%   corresponding to the custom storage class
%%
%function ClassAccess(record, request) void

  %switch request
    %case "setup"
      %assign numData = LibCustomStorageClassRecordNumData(record)
      %foreach idx = numData

	%% Get the idx'th data record with BitField storage
	%assign item = LibGetDataRecord(record, idx)
	
	%% Make sure the record is non-complex.
	%<LibCustomStorageVerifyRecordIsNonComplex(item)>

	%% Make sure function names and header files were specified
        %assign props = LibGetCustomStorageAttributes(item)
	%<LibCustomStorageVerifyPropertyIsNotEmpty(item, ...
	  props.GetFunction, "GetFunction")>
	%<LibCustomStorageVerifyPropertyIsNotEmpty(item, ...
	  props.SetFunction, "SetFunction")>
	%<LibCustomStorageVerifyPropertyIsNotEmpty(item, ...
	  props.HeaderFile, "HeaderFile")>

      %endforeach
      %return
      %break

    %case "comment"
      %return LibDefaultCustomStorageComment(record)
      %break

    %default
      %% Unknown access type.
      %return LibDefaultCustomStorageUnknownClassAccessType ...
	(record, request)
      %break
      
  %endswitch
      
%endfunction


%% Function: Version ============================================
%% Abstract:
%%
%function Version(record) void
  %return 2
%endfunction
