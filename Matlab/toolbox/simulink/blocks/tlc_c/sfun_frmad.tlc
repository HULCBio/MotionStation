%% $RCSfile: sfun_frmad.tlc,v $
%% $Revision: 1.3 $
%% $Date: 2002/04/10 18:17:15 $
%%
%% Abstract: Frame-based A/D Conversion
%%    An example of a frame-based A/D converter. Contains place-holders
%%    for adding actual code for a frame-based A/D converter
%%
%% Copyright 1990-2002 The MathWorks, Inc.

%implements sfun_frmad "C"

%% Function: BlockTypeSetup ====================================================
%% Abstract:
%%      Cache an extern declarations to frame-based AD routines
%%
%function BlockTypeSetup(block, system) void
  %openfile buffer
  #include "sfun_frmad_wrapper.h"
  %closefile buffer
  %<LibCacheExtern(buffer)>
%endfunction

%% Function: Start =============================================================
%%
%function Start(block, system) Output
  /* Frame-based A/D converter %<Name>:
   *  Set up converter for a frame size of %<SFcnParamSettings.FrameSize>
   * (For now, simply generate a noisy multi-channel frame-based signal).   
   */
  
%endfunction

%% Function: Outputs ===========================================================
%%
%function Outputs(block, system) Output
  /* Frame-based A/D converter  %<Name>:
   *  Start conversion and wait till it completes
   * (For now, simply generate a noisy multi-channel frame-based signal).
   */
   %assign y        = LibBlockOutputSignalAddr(0, "", "", 0)
   %assign fnName   = SFcnParamSettings.Function
   %assign frmSize  = SFcnParamSettings.FrameSize
   %assign ts       = SFcnParamSettings.Ts
   %assign noisA    = SFcnParamSettings.NoiseAmp
   %assign noisF    = SFcnParamSettings.NoiseFreq
   %assign nAmpRows = Parameter[0].Dimensions[0]
   %assign nAmpCols = Parameter[0].Dimensions[1]
   %assign nAmps    = nAmpRows * nAmpCols
   %assign count    = LibBlockIWork(IWORK, "", "", 0)
   %%
   %if fnName == "Constant"
     sfun_frmad_const_wrapper(%<y>, %<frmSize>, %<ts>, %<count>,
                              nAmps, %<LibBlockParameterAddr(Amplitude,"","",0)>,
			      %<noisA>, %<noisF>);
   %else
     %assign nFreqRows = Parameter[1].Dimensions[0]
     %assign nFreqCols = Parameter[1].Dimensions[1]
     %assign nFreqs    = nFreqRows * nFreqCols
     sfun_frmad_sine_wrapper(%<y>, %<frmSize>, %<ts>, %<count>,
                              %<nAmps>, %<LibBlockParameterAddr(Amplitude,"","",0)>,
			      %<nFreqs>, %<LibBlockParameterAddr(Frequency,"","",0)>,
			      %<noisA>, %<noisF>);
   %endif
   %<count> = %<count> + %<frmSize>;
   %%
%endfunction

%% [EOF] sfun_frmad.tlc
