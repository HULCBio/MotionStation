%% rtdx_util.tlc
%%
%% $Revision: 1.1.6.2 $ $Date: 2004/04/08 21:08:21 $
%% Copyright 2001-2003 The MathWorks, Inc.


%if !EXISTS("RTDX_UTIL_TLC")
    %assign RTDX_UTIL_TLC = 1

    %% Function:  CreateNewRTDXDefines ========================================
    %%
    %% Creates the file that redefines RTDX buffers
    %%
    %function CreateNewRTDXDefines() void

        %openfile srcFile = "MW_rtdx_buf.c"     
                
		/*************************************************************************
		* Defines buffers used by RTDX buffer manager.
		*************************************************************************/        
        
		#include <RTDX_access.h>        /* RTDX_CODE, RTDX_DATA                 */
		
        %if (EXISTS(::RTDXSrcSize) == TLC_TRUE && EXISTS(::RTDXSnkSize) == TLC_TRUE)
            %if ( ::RTDXSrcSize > ::RTDXSnkSize )
                %assign BufSzInBytes = ::RTDXSrcSize
            %else
                %assign BufSzInBytes = ::RTDXSnkSize            
            %endif
        %elseif (EXISTS(::RTDXSrcSize) == TLC_TRUE)
                %assign BufSzInBytes = ::RTDXSrcSize            
        %elseif (EXISTS(::RTDXSnkSize) == TLC_TRUE)
                %assign BufSzInBytes = ::RTDXSnkSize            
        %endif
        
        %assign BufSzInInts = BufSzInBytes/4
        %if (BufSzInInts*4 < BufSzInBytes)
            %assign BufSzInInts = BufSzInInts + 1
        %endif
        		
		#ifndef BUFRSZ
		#define BUFRSZ %<BufSzInInts+4> %% plus 2 Words - RTDX header and 2 Words extra
		#endif
		
		#if RTDX_USE_DATA_SECTION
		#pragma DATA_SECTION(RTDX_Buffer,".rtdx_data")
		#pragma DATA_SECTION(RTDX_Buffer_Start,".rtdx_data")
		#pragma DATA_SECTION(RTDX_Buffer_End,".rtdx_data")
		#endif
		
		int RTDX_DATA RTDX_Buffer[BUFRSZ];
		
		/*
         * The buffer used by RTDX is defined by 2 symbols: RTDX_Buffer_Start
         * and RTDX_Buffer_End.  We use the following declarations in order to
         * export these names
		*/
		
		const void RTDX_DATA *RTDX_Buffer_Start = &RTDX_Buffer[0];
		const void RTDX_DATA *RTDX_Buffer_End  = &RTDX_Buffer[BUFRSZ-1];
        %closefile srcFile
        
        %<LibAddToModelSources ("MW_rtdx_buf")>
		
    %endfunction  %% AddRtdxIncludesToModelH


    %% Function:  AddRtdxIncludesToModelH =====================================
    %%
    %% Adds headers necessary for rtdx
    %%
    %function AddRtdxIncludesToModelH(block) void

        %% Make sure we only define these quantities once,
        %% even if we're called multiple times.
        %%
        %if JustCreatedCompiledModelRecord("rtdx_util_includes")

            %openfile buffer
		    #include <rtdx.h>
		    %if useDSPBIOS == 0
			    %<CreateNewRTDXDefines()>
		    %endif
            %closefile buffer
            %<LibCacheIncludes(buffer)>

        %endif %% JustCreatedCompiledModelRecord
    %endfunction  %% AddRtdxIncludesToModelH


%endif %% RTDX_UTIL_TLC

%% [EOF] rtdx_util.tlc
