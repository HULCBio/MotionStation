%% $RCSfile: sdspwai2.tlc,v $
%% $Revision: 1.5.4.2 $ 
%% $Date: 2004/04/12 23:10:14 $
%%
%% Copyright 1995-2003 The MathWorks, Inc.
%%
%% Abstract: Gets audio samples from audio input device

%implements sdspwai2 "C"

%include "dsplib.tlc"

%% Function: BlockTypeSetup ==============================================
%function BlockTypeSetup(block, system) void

    %<Include_rt_header(block,"fromwavedevice_ex_win32")>
	
	%% copy needed source files to project directory and add to build
	
	%assign helper_source = "fromwavedevice_ex_win32"
	%assign utils_source  = "audio_utils_win32"
	%assign helper_c      = helper_source + ".c"
	%assign utils_c       = utils_source + ".c"

	%assign file_sep      = FEVAL("filesep")
	%assign source_dir    = FEVAL("fullfile",MATLAB_ROOT,"toolbox","dspblks","include",file_sep)
	%assign model_dir     = FEVAL("rtwprivate","rtwattic","getBuildDir") + file_sep

	%if ! LibAddToModelSources(helper_source) %% only copy it if it's not there already
		%<FEVAL("copyfile", "%<source_dir>%<helper_c>", "%<model_dir>%<helper_c>")>
	%endif

	%if ! LibAddToModelSources(utils_source)
		%<FEVAL("copyfile", "%<source_dir>%<utils_c>", "%<model_dir>%<utils_c>")>
	%endif

%endfunction %% BlockTypeSetup

%% Function: BlockInstanceSetup ==============================================
%%
%function BlockInstanceSetup(block, system) void

  %assign OUTPORT	    = 0

  %assign rate      = SFcnParamSettings.SampleRate
  %assign bits      = SFcnParamSettings.BitsPerSample
  %assign chans     = SFcnParamSettings.NumChannels
  %assign bufSize   = SFcnParamSettings.BufferSize
  %assign dataType  = LibBlockOutputSignalDataTypeId(OUTPORT)
  %assign bufLength = SFcnParamSettings.BufferLengthInSeconds
  %assign deviceID	= SFcnParamSettings.DeviceID
  %assign useMapper = SFcnParamSettings.UseWaveMapper
  %%

  %assign block = block + OUTPORT + rate + bits + chans + bufSize + dataType ...
        + bufLength + deviceID + useMapper 

%endfunction %% BlockInstanceSetup


%% Function: Start ==========================================================
%%
%function Start(block, system) Output

	/* Signal Processing Blockset From Wave Device (%<ParamSettings.FunctionName>) - '%<Name>' - Start */
	%<LibBlockDWork(FromWaveDevObj, "", "", 0)> = exMWDSP_Wai_Create(%<rate>, 
	    %<bits>, %<chans>, %<bufSize>, %<dataType>, %<bufLength>, %<deviceID>, 
		%<useMapper>);

	exMWDSP_Wai_Start(%<LibBlockDWork(FromWaveDevObj, "", "", 0)>);

%endfunction %% Start




%% Function: Outputs ==========================================================
%%
%function Outputs(block, system) Output

	/* Signal Processing Blockset From Wave Device (%<ParamSettings.FunctionName>) - '%<Name>' - Output */
	exMWDSP_Wai_Outputs(%<LibBlockDWork(FromWaveDevObj, "", "", 0)>, %<LibBlockOutputSignalAddr( OUTPORT,  "", "", 0)>);

%endfunction %% Outputs



%% Function: Update ==========================================================
%%
%function Update(block, system) Output

%endfunction %% Update




%% Function: Terminate ==========================================================
%%
%function Terminate(block, system) Output

	/* Signal Processing Blockset From Wave Device (%<ParamSettings.FunctionName>) - '%<Name>' - Terminate */
    exMWDSP_Wai_Terminate(%<LibBlockDWork(FromWaveDevObj, "", "", 0)>);

%endfunction %% Terminate



%% [EOF] sdspwafo2.tlc
