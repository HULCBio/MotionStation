%% $RCSfile: sdspwao2.tlc,v $
%% $Revision: 1.6.4.2 $ 
%% $Date: 2004/04/12 23:10:15 $
%%
%% Copyright 1995-2003 The MathWorks, Inc.
%%
%% Abstract: Sends input to WAVE audio output device

%implements sdspwao2 "C"

%include "dsplib.tlc"

%% Function: BlockTypeSetup ==============================================
%function BlockTypeSetup(block, system) void

    %<Include_rt_header(block,"towavedevice_ex_win32")>
	
	%% copy needed source files to project directory and add to build
	
	%assign helper_source = "towavedevice_ex_win32"
	%assign utils_source  = "audio_utils_win32"
	%assign helper_c      = helper_source + ".c"
	%assign utils_c       = utils_source + ".c"

	%assign file_sep      = FEVAL("filesep")
	%assign source_dir    = FEVAL("fullfile",MATLAB_ROOT,"toolbox","dspblks","include",file_sep)
	%assign model_dir     = FEVAL("rtwprivate","rtwattic","getBuildDir") + file_sep

	%if ! LibAddToModelSources(helper_source) %% only copy it if it's not there already
		%<FEVAL("copyfile", "%<source_dir>%<helper_c>", "%<model_dir>%<helper_c>")>
	%endif

	%if ! LibAddToModelSources(utils_source)
		%<FEVAL("copyfile", "%<source_dir>%<utils_c>", "%<model_dir>%<utils_c>")>
	%endif

%endfunction %% BlockTypeSetup

%% Function: BlockInstanceSetup ==============================================
%%
%function BlockInstanceSetup(block, system) void

  %assign INPORT	    = 0

  %assign deviceID	= SFcnParamSettings.DeviceID
  %assign buflen	= SFcnParamSettings.BufferLengthInSeconds
  %assign initDelay	= SFcnParamSettings.InitialDelayInSeconds
  %assign useMapper = SFcnParamSettings.UseWaveMapper
  %assign bits      = SFcnParamSettings.NumberOfBits
  %%
  %assign numDims   = LibBlockInputSignalNumDimensions(INPORT)
  %assign dims      = LibBlockInputSignalDimensions(INPORT)
  %assign isFrame   = LibBlockInputSignalIsFrameData(INPORT)
  %if isFrame
    %assign inputBufSize = dims[0]
    %assign numChannels  = (numDims == 2) ? dims[1] : 1
  %else
    %assign inputBufSize = 1
    %assign numChannels  = LibBlockInputSignalWidth(INPORT)
  %endif

  %assign sampleRate	 = inputBufSize / LibBlockSampleTime(block)
  
  %assign dataTypeID  = LibBlockInputSignalDataTypeId(INPORT)

  %assign block = block + INPORT + deviceID + buflen + initDelay + useMapper ... 
		+ inputBufSize + numChannels  + sampleRate  + dataTypeID + bits

%endfunction %% BlockInstanceSetup


%% Function: Start ==========================================================
%%
%function Start(block, system) Output

	/* Signal Processing Blockset To Wave Device (%<ParamSettings.FunctionName>) - '%<Name>' - Start */
	%<LibBlockDWork(ToWaveDeviceObj, "", "", 0)> = exMWDSP_Wao_Create(%<sampleRate>, %<bits>, %<numChannels>, 
				%<inputBufSize>, %<dataTypeID>, %<buflen>, %<initDelay>, %<deviceID>, %<useMapper>);
	exMWDSP_Wao_Start(%<LibBlockDWork(ToWaveDeviceObj, "", "", 0)>);

%endfunction %% Start




%% Function: Outputs ==========================================================
%%
%function Outputs(block, system) Output

%endfunction %% Outputs



%% Function: Update ==========================================================
%%
%function Update(block, system) Output

	/* Signal Processing Blockset To Wave Device (%<ParamSettings.FunctionName>) - '%<Name>' - Update */    
    exMWDSP_Wao_Update(%<LibBlockDWork(ToWaveDeviceObj, "", "", 0)>, %<LibBlockInputSignalAddr( INPORT,  "", "", 0)>);

%endfunction %% Update




%% Function: Terminate ==========================================================
%%
%function Terminate(block, system) Output

	/* Signal Processing Blockset To Wave Device (%<ParamSettings.FunctionName>) - '%<Name>' - Terminate */
    exMWDSP_Wao_Terminate(%<LibBlockDWork(ToWaveDeviceObj, "", "", 0)>);

%endfunction %% Terminate



%% [EOF] sdspwafo2.tlc
