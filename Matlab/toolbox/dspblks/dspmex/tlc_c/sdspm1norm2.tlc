%% $RCSfile: sdspm1norm2.tlc,v $
%% $Revision: 1.7.4.2 $ 
%% $Date: 2004/04/12 23:08:56 $
%%
%% Copyright 1995-2003 The MathWorks, Inc.
%%
%% Abstract: Matrix 1-Norm

%implements sdspm1norm2 "C"

%% Function: Outputs ==========================================================
%%
%function Outputs(block, system) Output
%%
%% All fixed-point blocks call the fixed-point setup function
%%
%<FixPt_Setup(block, system)>
%%
%%
%% Common setup code
%%
%assign INPORT  = 0
%assign OUTPORT = 0
%assign numDims = LibBlockInputSignalNumDimensions(INPORT)
%assign dims    = LibBlockInputSignalDimensions(INPORT)
%assign N       = (numDims == 2) ? dims[1] : 1
%assign M       = dims[0]
%assign cplx    = LibBlockInputSignalIsComplex(INPORT) != 0
%assign inDT    = FixPt_GetInputDataType(INPORT)
%assign outDT   = FixPt_GetOutputDataType(OUTPORT)
%%
/* Signal Processing Blockset Matrix 1-Norm (%<ParamSettings.FunctionName>) - '%<Name>' */
{
  %if FixPt_DataTypeIsFloat(inDT)
    %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
    %% FLOATING POINT CODE GENERATION             %%
    %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
    %%
    %% Floating-point-only setup code
    %%
    %assign dtype   = LibBlockInputSignalDataTypeName(INPORT,"")
    %assign dtype_c = LibBlockInputSignalDataTypeName(INPORT,"%<tRealPart>")
    %%
    const %<dtype> *u = %<LibBlockInputSignalAddr(INPORT, "", "", 0)>;
    %if N > 1
      %<dtype_c>  m1norm = 0.0;
      int_T   j;
      for(j=%<N>; j-- > 0; ) {
    %endif
    %<dtype_c> sumabsAj = 0.0;
    %if M > 1
      int_T  i;
      for(i=%<M>; i-- > 0; ) {
    %endif
    %if !cplx
      %% REAL
        %if dtype == "real32_T"
          real32_T temp = *u++;
          sumabsAj += FABS32(temp);
        %else
          sumabsAj += fabs(*u++);
        %endif
    %else
      %% COMPLEX
      %<dtype_c> cabsAij;
      const %<dtype> uval = *u++;
      %if dtype == "creal32_T"
        CABS32(uval, cabsAij);
      %else
        CABS(uval, cabsAij);
      %endif
      sumabsAj += cabsAij;
    %endif
    %if M > 1
    }
    %endif
    %if N > 1
      m1norm = MAX(m1norm, sumabsAj);
    }
    %<LibBlockOutputSignal(OUTPORT, "", "", 0)> = m1norm;
    %else
      %<LibBlockOutputSignal(OUTPORT, "", "", 0)> = sumabsAj;
    %endif
  %else
    %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
    %% FIXED-POINT CODE GENERATION                %%
    %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
    %%
    %% Fixed-point-only setup code
    %%
    %createrecord RndSatRec { roundingMode FixPtRoundingMode; ...
                              overflowMode FixPtSaturationMode}
    %%
    %assign       acc1Label = "acc1"
    %assign       accDT     = FixPt_GetParameterDataType(ACC_DATA_TYPE, 0, 0)
    %createrecord Acc1Rec    { label     acc1Label; ...
                              dTypeRec  accDT; ...
                              isComplex 0 }
    %if N > 1
    %assign       acc2Label = "acc2"
    %createrecord Acc2Rec   { label     acc2Label; ...
                              dTypeRec  accDT; ...
                              isComplex 0 }
    %endif
    %%
    %if (M == 1) && (N == 1)
      %% SCALAR input
      %assign inLabel = LibBlockInputSignal(INPORT, "", "", "0")
    %elseif (M == 1) && (N > 1)
      %% ROW vector input
      %assign inLabel = LibBlockInputSignal(INPORT, "j", "", "0")
    %elseif (M > 1) && (N == 1)
      %% COLUMN vector input
      %assign inLabel = LibBlockInputSignal(INPORT, "i", "", "0")
    %else
      %% FULL MATRIX input
      %% (M > 1) && (N > 1)
      %assign inLabel = LibBlockInputSignal(INPORT, "i+chanOffset", "", "0")
    %endif
    %createrecord InpRec  { label     inLabel; ...
                            dTypeRec  inDT; ...
                            isComplex cplx }
    %%
    %assign       outLabel = LibBlockOutputSignal(OUTPORT, "", "", "0")
    %createrecord OutRec   { label     outLabel; ...
                             dTypeRec  outDT; ...
                             isComplex 0 }
    %if N > 1
      /* Initialize value for first accumulated sum comparison */
      %<accDT.Name> %<acc2Label> = 0;

      /* Loop over input columns */
      int j;
      for (j=0; j<%<N>; j++) {
        %if (M > 1) && (N > 1)
          const int chanOffset = %<M> * j;

        %endif
    %endif
        %if (M > 1) && (N > 1)
          /* Compute accumulated sum of abs vals for this column */
        %endif
        %<accDT.Name> %<acc1Label> = 0;
        %if M > 1
          int i;
          for (i=0; i<%<M>; i++) {
        %endif
            if (%<inLabel> < 0) {
                %<DSPFixptLibMinusEquals(Acc1Rec, InpRec, RndSatRec)>
            } else {
                %<DSPFixptLibPlusEquals(Acc1Rec, InpRec, RndSatRec)>
            }
        %if M > 1
          }
        %endif
    %if N > 1

        /* Update matrix 1-norm output */
        %<acc2Label> = MAX(%<acc2Label>, %<acc1Label>);
        %<DSPFixptLibEquals(OutRec, Acc2Rec, RndSatRec)>
      }
    %else
      %<DSPFixptLibEquals(OutRec, Acc1Rec, RndSatRec)>
    %endif
  %endif  %% Floating-point vs fixed-point code
}
%%  %endif
  
%endfunction

%% [EOF] sdspm1norm2.tlc
