%% sdspsqdec.tlc
%% Abstract: TLC for Signal Processing Blockset block to do decoding for scalar quantization. (sdspsqdec)
%%
%% Copyright 1995-2003 The MathWorks, Inc.
%% $Revision: 1.1.6.3 $ $Date: 2003/12/06 15:28:05 $


%implements "sdspsqdec" "C"
%include "dsplib.tlc"

%% Function: BlockInstanceSetup ===============================================
%%
%function BlockInstanceSetup(block, system) void

  %assign INPORT_IDX = 0
  %assign INPORT_CB  = 1
  %assign OUTPORT_CW = 0

  %assign IdxDTypeName  = LibBlockInputSignalDataTypeName(INPORT_IDX,"")
  %assign IdxDTypeID    = LibBlockInputSignalDataTypeId(INPORT_IDX)
  %assign CBfromMask    = %<SFcnParamSettings.IsCBSourceMask>
  %assign InputWidth      = LibBlockInputSignalWidth(INPORT_IDX)
  %assign BytesPerOutputCWEle = SFcnParamSettings.BytesPerOutputCWEle %% see below
  %assign MaxAllowableZeroBasedIdxValue = SFcnParamSettings.MaxAllowableZeroBasedIdxValue
  %assign block         = block + INPORT_IDX  + INPORT_CB + OUTPORT_CW ...
                                + IdxDTypeName + IdxDTypeID + CBfromMask + InputWidth ...
				+ BytesPerOutputCWEle + MaxAllowableZeroBasedIdxValue
 
%endfunction %% BlockInstanceSetup


%% Function: Outputs ==========================================================
%%
%% Abstract:
%%
%function Outputs(block, system) Output
    %% 
    /* Signal Processing Blockset Scalar Quantizer Decoder:  (%<ParamSettings.FunctionName>) - '%<Name>' */
    {
	const %<IdxDTypeName>  *u_IdxPtr = %<LibBlockInputSignalAddr(INPORT_IDX, "", "", 0)>;
        %if (CBfromMask)
          const byte_T *u_CBPtr  = (const byte_T *)%<LibBlockParameterAddr(Codebook,"","",0)>; 
        %else
          const byte_T *u_CBPtr  = (const byte_T *)%<LibBlockInputSignalAddr(INPORT_CB, "", "", 0)>;
        %endif
        byte_T *y_CW = (byte_T *)%<LibBlockOutputSignalAddr(OUTPORT_CW, "", "", 0)>;
        %%
        %if InputWidth > 1
          int_T i;
          for (i = 0; i < %<InputWidth>; i++) {
              /* Clip the value of index if necessary */
              %<IdxDTypeName> validIdx = u_IdxPtr[i];
	      %%
	      %if (IdxDTypeID ==tSS_UINT8 || IdxDTypeID ==tSS_UINT16 || IdxDTypeID ==tSS_UINT32)
	       if (validIdx > %<MaxAllowableZeroBasedIdxValue>) validIdx = %<MaxAllowableZeroBasedIdxValue>;
              %else
               if (validIdx < 0) validIdx = 0;
               else if (validIdx > %<MaxAllowableZeroBasedIdxValue>) validIdx = %<MaxAllowableZeroBasedIdxValue>;
              %endif
              /* Output quantized value */
              memcpy((y_CW+ i*%<BytesPerOutputCWEle>), 
                     (u_CBPtr + validIdx *%<BytesPerOutputCWEle>), 
                     %<BytesPerOutputCWEle>
                    ); 
                                            }
        %else
            /* Clip the value of index if necessary */
	       %<IdxDTypeName> validIdx = u_IdxPtr[0];
	       %if (IdxDTypeID ==tSS_UINT8 || IdxDTypeID ==tSS_UINT16 || IdxDTypeID ==tSS_UINT32)
	        if (validIdx > %<MaxAllowableZeroBasedIdxValue>) validIdx = %<MaxAllowableZeroBasedIdxValue>;
               %else
	        if (validIdx < 0) validIdx = 0;
	        else if (validIdx > %<MaxAllowableZeroBasedIdxValue>) validIdx = %<MaxAllowableZeroBasedIdxValue>;
               %endif
	       /* Output quantized value */
	       memcpy(y_CW, 
		      (u_CBPtr + (int_T)validIdx*%<BytesPerOutputCWEle>), 
		      %<BytesPerOutputCWEle>
		     ); 
        %endif
    }
%endfunction %% Outputs

%% EOF: sdspsqdec.tlc
