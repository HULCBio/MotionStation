%% $RCSfile: sdspg711.tlc,v $
%% $Revision: 1.1.6.3 $ 
%% $Date: 2004/04/08 20:50:07 $
%%
%% Copyright 1995-2001 The MathWorks, Inc. 
%%
%% Abstract: Implements G.711 ITU-T recommendation

%implements sdspg711 "C"

%%include "dsplib.tlc"

%% Function: BlockTypeSetup ====================================================
%function BlockTypeSetup(block, system) void

    %<Include_rt_header(block,"dspg711_rt")>

%endfunction %% BlockTypeSetup


%% Function: Outputs ===========================================================
%%
%% Abstract:
%%      Generates code segment for the encoder block's output function.
%function Outputs(block, system) Output
  %%
  /* Signal Processing Blockset G.711 (%<SFcnParamSettings.Mode>) - '%<Name>' - Output */
  %assign mode = SFcnParamSettings.Mode
  %%
  %switch (mode)
    %case "PCM_2_A"
      %%
      %assign nElems = LibBlockOutputSignalWidth(0)
	MWDSP_G711EncASat(%<LibBlockInputSignalAddr(0,"","",0)>,
	                  %<LibBlockOutputSignalAddr(0,"","",0)>,
			  %<nElems>);
      %break
    %case "PCM_2_Mu"
      %%
      %assign nElems = LibBlockOutputSignalWidth(0)
	MWDSP_G711EncMuSat(%<LibBlockInputSignalAddr(0,"","",0)>,
	                  %<LibBlockOutputSignalAddr(0,"","",0)>,
			  %<nElems>);
      %break
    %case "A_2_PCM"
    %case "Mu_2_PCM"
      %%
      %assign rollVars   = ["U", "Y"]
      %assign rollRegion = [0:%<LibBlockOutputSignalWidth(0)-1>]
      %roll sigIdx=RollRegions, lcv=RollThreshold, block, "Roller", rollVars
	%assign u = LibBlockInputSignal( 0, "", lcv, sigIdx)
	%assign y = LibBlockOutputSignal(0, "", lcv, sigIdx)
	%assign p = LibBlockParameter(   Table, u, "", 0)	
	%<y> = %<p>;
      %endroll
      %break
    %case "A_2_Mu"
      %%
      %assign rollVars   = ["U", "Y"]
      %assign rollRegion = [0:%<LibBlockOutputSignalWidth(0)-1>]
      %roll sigIdx=RollRegions, lcv=RollThreshold, block, "Roller", rollVars
	%assign u  = LibBlockInputSignal(0, "", lcv, sigIdx)
	%assign y  = LibBlockOutputSignal(0, "", lcv, sigIdx)
	%assign u1 = u + "^0xD5"
	%assign u2 = u + "^0x55"
	%assign p1 = LibBlockParameter(Table, u1, "", 0)
	%assign p2 = LibBlockParameter(Table, u2, "", 0)
	%<y> = (%<u> & 0x80) ? (0xFF ^ %<p1>) : 
	       (0x7F ^ %<p2>);
      %endroll
      %break
    %case "Mu_2_A"
      %%
      %assign rollVars   = ["U", "Y"]
      %assign rollRegion = [0:%<LibBlockOutputSignalWidth(0)-1>]
      %roll sigIdx=RollRegions, lcv=RollThreshold, block, "Roller", rollVars
	%assign u  = LibBlockInputSignal(0, "", lcv, sigIdx)
	%assign y  = LibBlockOutputSignal(0, "", lcv, sigIdx)
	%assign u1 = u + "^0xFF"
	%assign u2 = u + "^0x7F"
	%assign p1 = LibBlockParameter(Table, u1, "", 0)
	%assign p2 = LibBlockParameter(Table, u2, "", 0)
	%<y> = (%<u> & 0x80) ? (0xD5 ^ (%<p1> - 1)) : 
	       (0x55 ^ (%<p2> - 1));
      %endroll
      %break
  %endswitch
  /* end of G.711 (%<SFcnParamSettings.Mode>) */
%endfunction  %% Outputs
