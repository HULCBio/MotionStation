%% $RCSfile: stic6x_autocor.tlc,v $
%% $Revision: 1.1.6.1 $
%% $Date: 2004/01/22 18:32:29 $
%%
%% Copyright 2001-2003 The MathWorks, Inc.
%%
%% Abstract: Autocorrelation (unbiased)

%implements stic6x_autocor "C"

%include "tiblksutil.tlc"

%% Function: BlockTypeSetup ==============================================
%function BlockTypeSetup(block, system) void

  %<Include_ti_header(block, "dsp_autocor")>

%endfunction %% BlockTypeSetup


%% Function: Outputs ==========================================================
%%
%function Outputs(block, system) Output
  %%
  %assign INPORT   = 0
  %assign OUTPORT  = 0
  %assign NX       = getInportSamps(INPORT)
  %assign numChans = getInportChans(INPORT)
  %assign outdims   = LibBlockOutputSignalDimensions(OUTPORT)
  %assign NR = (LibBlockInputSignalIsFrameData(INPORT))? outdims[0] : LibBlockOutputSignalWidth(OUTPORT)
  %%
  /* C%<chipID.Value[0]>x DSP Library autocor (%<ParamSettings.FunctionName>) - '%<Name>' */
  %%
  {
    %if numChans == 1
	/* Update delay line */
	memcpy(%<LibBlockDWorkAddr(INPUTBUFF, "", "", NR)>, %<LibBlockInputSignalAddr( INPORT , "", "", 0)>, %<2 * NX>);
	/* Compute Autocorrelation */
	DSP_autocor(%<LibBlockOutputSignalAddr(OUTPORT, "", "", 0)>, %<LibBlockDWorkAddr(INPUTBUFF, "", "", 0)>, %<NX>, %<NR>);
	/* Update Delay Line Buffer */
	memcpy(%<LibBlockDWorkAddr(INPUTBUFF, "", "", 1)>, %<LibBlockDWorkAddr(INPUTBUFF, "", "", NX+1)>, %<2 * (NR-1)>);
    %else
      int nChans = %<numChans>;
      short *ibuff = %<LibBlockDWorkAddr(INPUTBUFF, "", "", 0)>;
      short     *u = %<LibBlockInputSignalAddr( INPORT , "", "", 0)>;
      short     *r = %<LibBlockOutputSignalAddr(OUTPORT, "", "", 0)>;
      while (nChans--) {
	/* Update delay line */
	memcpy(ibuff + %<NR>, u, %<2 * NX>);
	/* Compute Autocorrelation */
	DSP_autocor(r, ibuff, %<NX>, %<NR>);
	u += %<NX>;
	r += %<NR>;
	/* Update Delay Line Buffer */
	memcpy(ibuff+1, ibuff + %<NX + 1>, %<2 * (NR-1)>);
	ibuff += %<NX + NR>;
      }
    %endif
  }
  
%endfunction

%% [EOF] stic6x_autocor.tlc

