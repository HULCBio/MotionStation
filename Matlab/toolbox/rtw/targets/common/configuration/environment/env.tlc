%% File: env.tlc
%% Abstract:
%%   TLC environment checking and setting routines.
%%
%% $Revision: 1.5.6.4 $
%% $Date: 2004/04/19 01:20:28 $
%%
%% Copyright 2002-2003 The MathWorks, Inc.

%if EXISTS("_ENVLIB_") == 0
%assign _ENVLIB_ = 1

%% function to call a tlc function which included in a tlc lib file
%% it doesnot require the tlc file in path
%% it don't support any parameters though
%function call_fcn(fcn_name, tlc_file_name) void
    %if !FILE_EXISTS("%<tlc_file_name>")
        %error Could not find required tlc file: %<tlc_file_name>
        %exit
    %else
        %include "%<tlc_file_name>"
        %return %<fcn_name>()
    %endif
%endfunction

%% Should be put into rtw/lib directory
%% Parent Class definition
%function chk_env(env_name) void
    %if ISFIELD(CompiledModel, "%<env_name>")
        %with CompiledModel
        %% the field should be a scalar, otherwise it 
        %% is not an env_name field
        %if SIZE(%<env_name>) != [1,1]
            %return TLC_FALSE
        %endif
        %if ISFIELD(%<env_name>, "chk_env_ptr")
            %assign chk_env_fcn = GETFIELD(%<env_name>, "chk_env_ptr")
            %if ISEQUAL("%<chk_env_fcn>", "EMPTY")
            %% no child implementation exists
                %return TLC_TRUE
            %else
            %% has child implemention
                %assign tlc_file_fullname = GETFIELD(%<env_name>, "tlc_file_ptr")
                %return call_fcn(chk_env_fcn, tlc_file_fullname)
            %endif
        %else
            %% must be true if cretaed via set_env() gateway 
            %return TLC_FALSE
        %endif
        %endwith
    %else
        %return TLC_FALSE
    %endif
%endfunction

%% Parent class constructor
%function set_env(env_name) void
%addtorecord CompiledModel \
    TargetName { Name "%<env_name>" }
%addtorecord CompiledModel \
    %<env_name> \
    {	tlc_file_ptr   "EMPTY" \
        chk_env_ptr    "EMPTY" \
        tlc_config_dir "" \
    }
    %return env_name
%endfunction

%% Allow reading of various fields in the targets environment record.
%function get_env(fieldname) void
  %if !ISFIELD(CompiledModel, "TargetName")
     %return ""
  %endif
  %assign env_name = CompiledModel.TargetName.Name
  %if ISFIELD(CompiledModel, "%<env_name>")
    %with CompiledModel
      %return GETFIELD(%<env_name>, "%<fieldname>")
    %endwith
  %else
    %return ""
  %endif
%endfunction

%endif %%_ENVLIB_
