%% File: canrtwinfo.tlc
%%
%% Abstract: Helper functions to allow CAN blocks to query target specific information.
%%
%% $Revision: 1.1.6.3 $
%% $Date: 2004/04/19 01:19:41 $
%%
%% Copyright 2002-2003 The MathWorks, Inc.

%% FUNCTIONS ==================================================================
%%
%%     GetModelName
%%     GetRTWWordLengths
%%     GetRTWCImplementation
%%
%%     LittleEndianString
%%     BigEndianString
%%
%% ============================================================================

%% Function: LittleEndianString =======================
%%
%% Returns a string used to denote a Little Endian target
%%
%function LittleEndianString()
   %return "LittleEndian"
%endfunction

%% Function: BigEndianString
%%
%% Returns a string used to denote a Big Endian target
%%
%function BigEndianString()
   %return "BigEndian"
%endfunction

%% Function: GetModelName ================================
%%
%% Returns a string containing the name of the model being built
%%
%function GetModelName()
   %if Accelerator
      %% for accelerator mode
      %% we have to strip off the _acc part of the name
      %assign name = ""
      %foreach character = SIZE(CompiledModel.Name, 1) - 4
         %assign name = "%<name>%<CompiledModel.Name[character]>"
      %endforeach
      %return name
   %else
      %% normal build, or right click build
      %% work ok with either the original model name,
      %% or the new right click model name
      %return "%<CompiledModel.Name>"
   %endif
%endfunction

%% Function: GetRTWWordLengths ==================================
%%
%% Returns structure containing RTW Word Length Information
%% 
%function GetRTWWordLengths()
   %assign TARGET_WORD_LENGTHS = FEVAL("rtwprivate", "rtwwordlengths", GetModelName())
   %if !EXISTS(TARGET_WORD_LENGTHS.WordSize)
      %exit EightBytePackingUtils.tlc: rtwwordlengths structure is missing WordSize field: %<TARGET_WORD_LENGTHS>
   %endif
   %return TARGET_WORD_LENGTHS
%endfunction 

%% Function: GetRTWCImplementation ==================================
%%
%% Returns structure containing RTW C Implementation information
%% 
%function GetRTWCImplementation()
   %assign TARGET_C_IMPLEMENTATION = FEVAL("rtwprivate", "rtw_implementation_props", GetModelName())
   %if !EXISTS(TARGET_C_IMPLEMENTATION.Endianess)
      %exit EightBytePackingUtils.tlc: rtw_implementation_props structure is missing Endianess field: %<TARGET_C_IMPLEMENTATION>
   %endif
   %if (TARGET_C_IMPLEMENTATION.Endianess != LittleEndianString()) && (TARGET_C_IMPLEMENTATION.Endianess != BigEndianString())
      %exit EightBytePackingUtils.tlc: Unrecognised or unsupported endianess string found in hardware settings: %<TARGET_C_IMPLEMENTATION.Endianess>
   %endif
   %return TARGET_C_IMPLEMENTATION
%endfunction
